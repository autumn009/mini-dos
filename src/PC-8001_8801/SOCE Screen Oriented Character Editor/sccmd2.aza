;"module of sccmd.aza
;
COLDENT:
COLDST:
; LD SP,STACK
 PUSH HL
 IF DEBUG
  LD HL,ZZZZZZ+1
 ELSE
  IF FILEVER
   LD HL,ZZZZZZ+1
  ELSE
   LD HL,ZZZZZZ/256*256+256+1 ;ZZZZZZ and $ff00+$100+1
  ENDIF
 ENDIF
 LD (TXTTOP),HL
 LD (POINTR),HL
 LD (POINTR2),HL
 IF FILEVER
  LD DE,$C7FF
 ELSE
  LD DE,$9000
 ENDIF
 EX DE,HL
 OR A
 SBC HL,DE
 LD (BUFSIZ),HL
 CALL CLRTXTSUB
; XOR A
; LD (CASEFLG),A
 IF FILEVER
 ELSE
  CALL AZACHK
  CALL AZATOP+18
 ENDIF
 POP HL
 IF DEBUG
  SRV CPRNXT
  DEFB "File Name:",0
  SRV CINLIN
 ENDIF
LOADTXT: LD IX,FCB1
 SRV MAKFCB
 LD A,(IX+1)
 CP " "
 JP Z,NONAM
 CALL SETAZA
;
 LD B,8
 LD A,0 ;CHR INPUT
 LD HL,SYSBUF
 SRV OPEN
 JR C,COLDNEW
;
 SRV CPRNXT
 DEFB "Loading Text...",$D,$A,0
;
 LD HL,(TXTTOP)
COLD05:
 SRV GETCHR
 CP 10
 JR Z,COLD05
 CP $1A 
 JR Z,COLD10
 LD (HL+),A
 JR COLD05
COLD10:
 LD (HL),0
 SRV CLOSE
 JR COLDSS
;
NONAM:SRV CPRNXT
 DEFB "No File Name",$D,$A,0
SERCOM:XOR A
 LD ($100),A
 SRV WARM
;
COLDNEW:CALL CLRTXTSUB
 SRV CPRNXT
 DEFB $D,$A,"***** New File *****",7,$D,$A,0
COLDSS:
;
 DEFB $EF,CPRNXT
 IF FILEVER
  DEFB $D,$A,"ED V1.1",252,252,252,$D,$A,0
 ELSE
  DEFB $D,$A,"EA V1.1",252,252,252,$D,$A,0
 ENDIF
 XOR A
 LD (POSITION),A
; LD (WINFLG),A
STATCMD:
 CALL STATUS
WARMST:
 LD SP,STACK
 CALL PRCRLF
 JP WARMM
;
SPKEP:DEFW 0
;
RETFILT:LD HL,(FILTKEP)
 LD SP,(SPKEP)
 RET
FILTENT:
COMAND:
CMD100:
 LD (FILTKEP),HL
 LD (SPKEP),SP
 LD A,(HL)
 INC HL
 CALL UPPER
 OR A
 JR Z,RETFILT
 CP 32
 JR Z,CMD100
 CP "$"
 JR Z,CMD100
 CP "?"
 JR Z,CMD100
 CP "."
 JR Z,STATCMD
 CP "E"
 JP Z,EDTCMD
 CP "F"
 JP Z,FINDCMD
 IF FILEVER
 ELSE
  CP "A"
  JP Z,AZA
  CP "Z"
  JP Z,AZA
  CP "Q"
  JP Z,AZA999
 ENDIF
 CP "C"
 JP Z,CLRTXT
 CP "B"
 JP Z,CHGTXT
 CP "L"
 JP Z,LODTXT
 CP "S"
 JP Z,SAVTXT
 CP "H"
 JP Z,HDIV
 CP "V"
 JP Z,VDIV
 CP "T"
 JP Z,FORMSW
 CP "M"
 JP Z,MARK
 CP "!"
 JP NZ,RETFILT
 CALL CLOSEENT
 IF DEBUG
  SRV DOSCMD
 ELSE
  SRV WARM
 ENDIF
;
SAVE1SUB:
 LD IX,FCB1
 LD DE,FCB2
 LD B,12
COLD20:LD A,(IX+0)
 INC IX
 LD (DE+),A
 DJNZ COLD20
 LD IX,FCB2
 CALL SETBAK
 SRV CPRNXT
 DEFB "Making Backup File(.bak)",$D,$A,0
COLD30:LD IX,FCB1
 LD IY,FCB2
 SRV RENAME
 JR NC,SAVE2SUB
 CP 1
 JR NZ,SAVE2SUB
 LD IX,FCB2
 SRV ERASE
 JR NC,COLD30
INTERR: SRV CPRNXT
 DEFB "Inter err",$D,$A,0
 JP SERCOM
SAVE2SUB:
 LD IX,FCB1
 SRV DSKFRE
 LD A,H
 OR A
 JR NZ,SAVE3SUB
 LD H,L
 LD L,0
 PUSH HL
 LD HL,(TXTTOP)
 LD BC,0
SV2S10:LD A,(HL+)
 OR A
 JR Z,SV2S40
 CP 13
 JR Z,SV2S20
 INC BC
 JR SV2S10
SV2S20:INC BC
 INC BC
 JR SV2S10
SV2S40:INC BC
 POP HL
 OR A
 SBC HL,BC
 JR NC,SAVE3SUB
 SRV CPRNXT
 DEFB "DISK FULL!!!!!!",$D,$A,7
 DEFB ">>> DELETING Backup File(.bak)",$D,$A,0
 LD IX,FCB2
 SRV ERASE
 LD IX,FCB1
 JR NC,SAVE3SUB
 JP INTERR
;
SAVE3SUB:PUSH IX
 SRV CPRNXT
 DEFB "Saving Text...",$D,$A,0
 POP IX
 LD A,1
 LD B,8
 LD HL,SYSBUF
 SRV OPEN
 LD HL,(TXTTOP)
CLE10:LD A,(HL+)
 OR A
 JR Z,CLE40
 CP 13
 JR Z,CLE20
 SRV PUTCHR
 JR CLE10
CLE20:SRV PUTCRLF
 JR CLE10
CLE40:LD A,$1A
 SRV PUTCHR
 SRV CLOSE
 RET
;
CLOSEENT:
 PUSH IX
 PUSH HL
 PUSH DE
 PUSH BC
 PUSH AF
 CALL SAVE1SUB
;
 XOR A
 LD ($100),A
 POP AF
 POP BC
 POP DE
 POP HL
 POP IX
 RET
;
OPTENT:
 SRV CPRNXT
 IF FILEVER
  DEFB "*ed READY",$D,$A,0
 ELSE
  DEFB "*ea READY",$D,$A,0
 ENDIF
 RET
;
AZA:
 PUSH HL
 CALL AZACHK
 POP HL
 JP AZATOP+9
AZA999:
 CALL AZACHK
 JP AZATOP+15
AZACHK:
 LD HL,AZATOP+6
 LD A,(HL)
 CP "A"
 JP NZ,UNDERR
 INC HL
 LD A,(HL)
 CP "z"
 JP NZ,UNDERR
 INC HL
 LD A,(HL)
 CP "a"
 JP NZ,UNDERR
 RET
;
SKIPNXT:
;"DE=scan str
;"HL=pointr
;"destroy B reg
 LD A,(DE+)
 OR A
 RET Z
 LD B,A
 LD A, (HL+)
 CALL UPPER
 CP B
 JR Z,SKIPNXT
 JP RETFILT
;
SKIPSPC:LD A,(HL+)
 CP 32
 JR Z,SKIPSPC
 DEC HL
 RET
;
CLRTXT:
 LD DE,LEARMES
 CALL SKIPNXT
 CALL CLRTXTSUB
 XOR A
 LD ($100),A
 SRV WARM
LEARMES:DEFB "LEAR",0
;
CLRTXTSUB:
 LD HL,(TXTTOP)
 LD (POINTR),HL
 LD (POINTR2),HL
 LD (HL),13
 INC HL
 XOR A
 LD (HL),A
 LD (POSITION),A
 LD (POSITION2),A
  LD (MARKFLG),A
 RET
;
CHGTXT:
 LD DE,UFMES
 CALL SKIPNXT
 CALL SKIPSPC
 CALL GETDEC
 LD (BUFSIZ),IX
 CP ","
 JP NZ,WARMM
 CALL GET4H
 PUSH IX
 POP BC
 LD A,B
 OR C
 JR Z,CHGTXEE
 INC IX
 LD (TXTTOP),IX
 LD (POINTR),IX
 LD (POINTR2),IX
 XOR A
 LD (POSITION),A
 LD (POSITION2),A
CHGTXEE:SRV WARM
UFMES:DEFB "UF",0
;
LODTXT:LD DE,OADMES
 CALL SKIPNXT
 PUSH HL
 CALL SAVE1SUB
 CALL CLRTXTSUB
 POP HL
 JP LOADTXT
;
SOUNDSW:
 INC HL
 LD A,(HL+)
 CALL UPPER
 CP "F"
 JR Z,SSWOFF
 CP "N"
 JP NZ,RETFILT
 DEFB $3E
SSWOFF:XOR A
 LD (SSW),A
 SRV WARM
;
SSW:DEFB 1
;
SAVTXT:
 LD A,(HL)
 CALL UPPER
 CP "O"
 JR Z,SOUNDSW
 LD DE,AVEMES
 CALL SKIPNXT
 LD IX,FCB2
 SRV MAKFCB
 LD IY,FCB1
 LD A,(IX+0)
 OR A
 CALL Z,TRNDVS
 LD A,(IX+1)
 CP " "
 CALL Z,TRNFN
 LD A,(IX+9)
 CP " "
 CALL Z,TRNFT
 CALL SAVE3SUB
 SRV WARM
;
OADMES:DEFB "OAD",0
AVEMES:DEFB "AVE",0
DIVMES:DEFB "DIV",0
;
VDIV:DEFB $3E
HDIV:XOR A
 PUSH AF
 LD DE,DIVMES
 CALL SKIPNXT
 POP AF
 LD (DIVMODE),A
 CALL RESWIN
 SRV WARM
DIVMODE:DEFB 1
;
FORMSW:LD A,(HL+)
 CALL UPPER
 CP "O"
 JP NZ,RETFILT
 LD A,(HL+)
 CALL UPPER
 CP "F"
 JR Z,FSWOFF
 CP "N"
 JP NZ,RETFILT
 DEFB $3E
FSWOFF:XOR A
 LD (FORMFLG),A
 SRV WARM
FORMFLG:DEFB 1
;
MARK:LD DE,ARKMES
 CALL SKIPNXT
 LD HL,(ERRMARK)
 LD DE,(TXTTOP)
 ADD HL,DE
 LD (POINTR),HL
 CALL ADJPNT
 SRV WARM
ARKMES:DEFB "ARK",0
;
TRNDVS:LD A,(IY+0)
 LD (IX+0),A
 RET
TRNFN:PUSH IY
 PUSH IX
 POP DE
 POP HL
 INC DE
 INC HL
 LD BC,8
 LDIR
 RET
TRNFT:PUSH IX
 LD BC,9
 POP HL
 ADD HL,BC
 EX DE,HL
 PUSH IY
 POP HL
 ADD HL,BC
 LD BC,3
 LDIR
 RET
;
GET4H:
 LD IX,0
CTX100:
 LD A,(HL)
 INC HL
 CALL UPPER
 CALL CHGHEX
 RET NC
 ADD IX,IX
 ADD IX,IX
 ADD IX,IX
 ADD IX,IX
 LD E,A
 LD D,0
 ADD IX,DE
 JR CTX100
;
GETDEC:
 LD IX,0
GDC100:
 LD A,(HL)
 INC HL
 CALL TEST09
 RET C
 SUB "0"
 PUSH IX
 POP DE
 ADD IX,IX
 ADD IX,IX
 ADD IX,DE
 ADD IX,IX
 LD E,A
 LD D,0
 ADD IX,DE
 JR GDC100
;
;
UNDERR:LD HL,UNDMES
 JR ERRORS
ERRORS:
 CALL PRCRLF
 CALL PRSTR
 CALL PRCRLF
 JP WARMM
;
UNDMES:DEFB "Undef",0
;
RESWIN: LD HL,PAR0
 LD DE,PARC
 LD BC,5
 LDIR
 XOR A
 LD (WINFLG),A
 RET
;
