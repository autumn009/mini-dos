;R7
;"MINI-DOS V0.8
;" by Aki
;
FALSE EQU 0
TRUE EQU $FFFF
;
DEBUG EQU FALSE
;
 IF DEBUG
ORIGIN EQU $9000
 ELSE
ORIGIN EQU $D000
 ENDIF
;
STARTLOC EQU ORIGIN
;
SYWORK:EQU $F220
STACK:EQU $FFF0
;
;"FCB offsets
DVS EQU 0
ATRB EQU 12
MODE EQU 13
BFTL EQU 15
BFTH EQU 16
BFS EQU 17
BFPL EQU 18
BFPH EQU 19
BFCL EQU 20
BFCH EQU 21
DRC EQU 22
NCL EQU 23
SCC EQU 24
FPTL EQU 25
FPTH EQU 26
FCL EQU 27
;
;
LINBUF EQU $EC96
;
FKBUF EQU $EA7C
CRTCBUF EQU $FF70
;
;
;"Disk Basic work
TRSECS EQU $EDB9
ERRCNT EQU $EDC5
DRVNO EQU $EDC6
DRIVES EQU $EDC7
CURX EQU $EA64
SHFTINF EQU $EA6A
NULL EQU $EA5A
;
;
TPA EQU $100
;
;
;
 ORG STARTLOC
.PHASE ORIGIN
;
BF2K:EQU ORIGIN-2048
;
 JP DOSENT
 JP DOSENT ;DUMMY
BKROM0:DEFB 0
BKRAM0:DEFB $80
BKPADR:DEFB $78
;
START:
 XOR A
 LD (ERRCNT),A
 RST $28
 DEFB 7 ;CIFCB
 LD (IX+0),$F0
 RST $28
 DEFB 8 ;COFCB
 LD (IX+0),$F0
 CALL PRCRLF
 LD HL,0
 LD (FATFLG),HL
 LD (FATFLG+2),HL
;
DOSCMD:
;
DOSCMD0:
 LD SP,STACK
 CALL PRCRLF
 CALL TSTFLC
 CALL Z,TPA+$F
 LD A,"$"
; CALL PRCHR
 CALL PRCHR
 CALL LINP
 LD A,(HL)
 CP $1A
 JP Z,REDCINEOF
 JP C,DOSCMD
 PUSH HL
 LD A,(IX+0)
 CP $F0
 JR NZ,DCM000
 LD IX,(COFCB)
 LD A,(IX+0)
 CP $F0
 JR Z,DOSCMDSUB
DCM000:PUSH AF
 CALL NZ,PRSTR
 POP AF
 CALL NZ,PRCRLF
 POP HL
;
DOSCMDSUB:
 CALL TSTFLC
 CALL Z,TPA+9
DCM10:LD A,(HL)
 INC HL
 CALL UPPER
 CALL TST09
 JP NC,COMCMD
;
 PUSH HL
 LD HL,DCMTBL
 LD B,A
DCM20:LD A,(HL)
 INC HL
 OR A
 JR Z,CMDERR
 CP B
 JR Z,DCMGO
 INC HL
 INC HL
 JR DCM20
DCMGO:LD E,(HL)
 INC HL
 LD D,(HL)
 LD A,B
 EX DE,HL
 EX (SP),HL
 RET
;
CMDERR:
 CALL PRNXT
 DEFB 30,"?",$D,$A,0
 JP DOSCMD
;
DCMTBL:
 DEFB $1A
 DEFW DOSENT
 DEFB "B"
 DEFW VBASIC
 DEFB "!"
 DEFW $100
 DEFB "?"
 DEFW DCM10
 DEFB "$"
 DEFW DCM10
 DEFB ":"
 DEFW COMCMD2
 DEFB "D"
 DEFW DIRCMD
 DEFB "T"
 DEFW TYPCMD
 DEFB "S"
 DEFW SAVCMD
 DEFB "L"
 DEFW LODCMD
 DEFB "K"
 DEFW KILCMD
 DEFB "R"
 DEFW RESCMD
 DEFB "N"
 DEFW NAMCMD
 DEFB "<"
 DEFW REDCIN
 DEFB ">"
 DEFW REDCOUT
 DEFB 0
;
REDCOUT:
 LD A,(HL)
 OR A
 JR Z,REDCOUTEOF
 LD IX,(COFCB)
 PUSH HL
 CALL WRCLOS
 POP HL
 CALL MAKFCB
 LD HL,REDCOUTBUF
 LD B,1
 LD A,1
 SRV 5 ;OPEN
 JP DOSCMD
;
REDCOUTEOF:
 LD IX,(COFCB)
 LD A,$1A
 CALL WRCHR
 CALL WRCLOS
 LD (IX+0),$F0 ;CONSOLE
 JP DOSCMD
;
REDCIN:
 PUSH HL
 LD IX,(CIFCB)
 CALL RDCLOS
 POP HL
 CALL MAKFCB
 CALL SETBAT
 LD HL,REDCINBUF
 LD B,1
 LD A,0
; CALL FCBSET
 SRV 5 ;OPEN
 JP DOSCMD
;
REDCINEOF:LD IX,(CIFCB)
 CALL RDCLOS
 LD (IX+0),$F0
 JP DOSCMD
;
DIRCMD:LD A,(HL)
 CALL UPPER
 CP "S"
 JP Z,DSTAT
 LD A,(DRIVES)
 CP 2
 JR C,DIRC05
 LD A,2
DIRC05:
 LD B,A
 LD A,(HL)
 SUB $31
 CP B
 JP C,DIRC10
 XOR A
DIRC10:LD (DRVNO),A
 INC HL
 LD IX,CFCB
 PUSH AF
 CALL MAKFCB
 POP AF
 INC A
 LD (IX+0),A
 CALL DIRSUB
 JP DOSCMD
;
DIRSUB:CALL SCHRES
DSB10:CALL NXTDIR
 JR C,DSB20
 CALL DSPDIR
 JR DSB10
DSB20:
 CALL PRCRLF
 CALL PRNXT
 DEFB "***** Drive: ",0
 LD A,(DRVNO)
 INC A
 LD L,A
 LD H,0
 CALL PRDEC
 CALL PRNXT
 DEFB ", Bytes Remaining: ",0
 CALL FREECR
 LD L,A
 LD H,0
 ADD HL,HL
 CALL PRDEC
 LD A,"K"
 CALL PRCHR
 RET
;
DSPDIR:PUSH HL
 PUSH IX
DDR10:LD A,(CURX)
 CP 1
 JR NZ,DDR15
 LD A,(DRVNO)
 ADD "1"
 CALL PRCHR
DDR15:CALL PRNXT
 DEFB ":",0
 LD B,6
DDR20:LD A,(HL)
 INC HL
 CALL PRCHR
 DJNZ DDR20
 LD A,"."
 CALL PRCHR
 LD B,3
DDR22:LD A,(HL)
 INC HL
 CALL PRCHR
 DJNZ DDR22
;
 CALL PRSPC
 INC HL
 LD A,(HL)
 LD E,0
DDR24:
 LD C,A
 AND $F0
 CP $C0
 JR NZ,DDR26
 LD A,E
 CP 50
 JR NC,DDR25
 CALL PRSPC
 CP 5
 JR NC,DDR25
 CALL PRSPC
DDR25:LD L,E
 LD H,0
 ADD HL,HL
 CALL PRDEC
 LD A,"K"
 CALL PRCHR
 CALL PRSPC
 JR DDR29
;
DDR26:
 INC E
 CALL FATAD
 LD B,0
 ADD HL,BC
 LD A,(HL)
 JR DDR24
;
DDR29:
 CALL PRSPC
 LD A,(CURX)
 CP 60
 CALL NC,PRCRLF
 POP IX
 POP HL
 RET
;
FREECR:
 CALL FATAD
 LD B,80*2
 LD C,0
FCR10:
 LD A,(HL)
 INC HL
 INC A
 JR NZ,FCR20
 INC C
FCR20:
 DJNZ FCR10
 LD A,C
 RET
;
DSTAT:
 INC HL
 PUSH HL
 LD IX,CFCB2
 CALL MAKFCB
 POP HL
 LD IX,CFCB
 CALL MAKFCB
 CALL RESDRV
 CALL DST10
 JP DOSCMD
DST10:
 CALL STPESC
 CALL SCHDIR
 JP C,DSB20
 CALL DSPDIR
 PUSH HL
 POP IY
 PUSH IX
 LD B,(IY+9)
 BIT 4,B
 LD A,"p"
 CALL NZ,PRCHR
 BIT 6,B
 LD A,"r"
 CALL NZ,PRCHR
 BIT 7,B
 LD A,"b"
 CALL NZ,PRCHR
 BIT 0,B
 LD A,"i"
 CALL NZ,DSTIMG
 CALL PRCRLF
 POP IX
 JR DST10
;
DSTIMG:
 CALL PRNXT
 DEFB "i :$",0
 LD IX,CFCB2
 CALL KEEPDP
 CALL TOFCB
 CALL SYSSET
 CALL RDOPENE
 CALL DST4H
 PUSH IX
 CALL PR4HEX
 CALL PRNXT
 DEFB "-$",0
 POP IX
 CALL DST4H
; CALL RDCLOS
 DEC HL
 CALL PR4HEX
 CALL RELODDP
 CALL RELODDIR
 RET
;
DST4H:
 CALL RDCHR
 LD L,A
 CALL RDCHR
 LD H,A
 RET
;
TYPCMD:
 LD IX,CFCB
 CALL MAKFCB
 CALL SYSSET
 CALL RDOPENE
 JP C,FNFERR
TPC10:CALL RDCHR
 JR C,TPC20
 CP $1A ;EOF MARK
 JR Z,TPC20
 CALL PRCHR
 CALL STPESC
 JR TPC10
TPC20:CALL RDCLOS
 JP DOSCMD
;
KILCMD:
 LD DE,ILLMES
 CALL SKIPNXT
 CP " "
 JP NZ,CMDERR
 LD IX,CFCB
 CALL MAKFCB
 CALL SERASE2
 JP C,FNFERR
 CALL PRNXT
 DEFB " Erased!",0
 JP DOSCMD
ILLMES:DEFB "ILL",0
;
RESCMD:LD DE,ESETMES
 CALL SKIPNXT
 CALL SRESET
 CALL STEXTINI
 CALL FLCCLOS
 JP DOSCMD
ESETMES:DEFB "ESET",0
;
NAMCMD:LD DE,AMEMES
 CALL SKIPNXT
 LD IX,CFCB
 CALL MAKFCB
 CP ","
 JP NZ,CMDERR
 INC HL
 LD IX,CFCB2
 CALL MAKFCB
 LD IX,CFCB
 LD IY,CFCB2
 CALL SRENAME
 JP NC,DOSCMD
 DEC A
 JP Z,EXSERR
 JP FNFERR
;
AMEMES:DEFB "AME",0
;
;
COMCMD:DEC HL
COMCMD2:
 CALL FLCCLOS
 LD IX,CFCB
 CALL MAKFCB
 LD (WK),HL
 CALL SETQQQ
 CALL SYSSET
 CALL RDOPENE
 LD HL,OVRMES
 CALL CHKTYP
 LD DE,(SYSBUF)
 JR NC,CCM020
 LD HL,TRCMES
 CALL CHKTYP
 LD DE,TPA
 JR NC,CCM010
 LD HL,FLCMES
 CALL CHKTYP
 JP C,FNFERR
 LD DE,TPA
CCM010:
CCM020:EX DE,HL
 LD A,(IX+ATRB)
 AND 1
 JR Z,CCM20
 LD B,4
CCM10:CALL RDCHR
 DJNZ CCM10
CCM20:
 PUSH HL
CCM30:CALL RDCHR
 JR C,CCM40
 LD (HL),A
 INC HL
 JR CCM30
CCM40:CALL RDCLOS
 POP HL
 PUSH HL
 LD A,(HL)
 INC HL
 CP $C3
 JR NZ,CCM50
 LD A,(HL)
 INC HL
 OR A
 JR NZ,CCM50
 LD A,(HL)
 INC HL
 OR A
 JR NZ,CCM50
 POP AF
 PUSH HL
 LD HL,(WK)
 RET
CCM50:LD HL,(WK)
 RET
;
SAVCMD:
 LD A,(HL)
 INC HL
 CALL UPPER
 LD IX,CFCB
 CP "T"
 JP NZ,SVICMD
 CALL MAKFCB
 CALL SETAZA
 CALL SYSSET
 CALL WROPEN
 LD HL,($FF60)
SVT10:
 LD A,(HL)
 INC HL
 OR A
 JR Z,SVTEE
 CALL WRCHR
 CP 13
 LD A,10
 CALL Z,WRCHR
 JR SVT10
SVTEE:
 LD A,$1A
 CALL WRCHR
 CALL WRCLOS
 JP DOSCMD
;
LODCMD:
 LD A,(HL)
 INC HL
 CALL UPPER
 CP "T"
 JP NZ,LDICMD
 LD BC,($FF60)
 PUSH BC
 LD A,(HL)
 CALL UPPER
 CP "A"
 JR NZ,LDT05
 INC HL
 POP BC
 PUSH HL
 LD L,C
 LD H,B
 LD BC,$FFFF
 XOR A
 CPIR
 DEC HL
 EX (SP),HL
LDT05:LD IX,CFCB
 CALL MAKFCB
 CALL SETAZA
 CALL SYSSET
 CALL RDOPENE
 POP HL
LDT10:CALL RDCHR
 CP 10
 JR Z,LDT10
 CP $1A
 JR Z,LDT20
 LD (HL),A
 INC HL
 JR LDT10
LDT20:LD (HL),0
 CALL RDCLOS
 JP DOSCMD
;
SETBAT:PUSH HL
 LD HL,BATMES
 CALL SETCOM
 POP HL
 RET
SETAZA:PUSH HL
 LD HL,AZAMES
 CALL SETCOM
 POP HL
 RET
;
SETIMG:PUSH HL
 LD HL,IMGMES
 CALL SETCOM
 POP HL
 RET
;
SETQQQ:
 PUSH HL
 LD HL,QQQMES
 CALL SETCOM
 POP HL
 RET
SETFKY:LD HL,FKYMES
;
SETCOM:
 PUSH AF
 PUSH IX
 LD A,(IX+9)
 CP " "
 JR NZ,STC20
SETCOM2:PUSH BC
 LD B,3
STC10:LD A,(HL)
 INC HL
 LD (IX+9),A
 INC IX
 DJNZ STC10
 POP BC
STC20:
 POP IX
 POP AF
 RET
;
BATMES:DEFB "bat"
AZAMES:DEFB "aza"
IMGMES:DEFB "img"
FKYMES:DEFB "fky"
TRCMES:DEFB "trc"
OVRMES:DEFB "ovr"
FLCMES:DEFB "flc"
QQQMES:DEFB "???"
;
;"HL:ptr,DE:scn
SKIPNXT:
 LD A,(DE)
 OR A
 LD A,(HL)
 RET Z
 CALL UPPER
 EX DE,HL
 CP (HL)
 INC HL
 EX DE,HL
 JP NZ,CMDERR
 INC HL
 JR SKIPNXT
;
;
SVICMD:
 CP "K"
 JP Z,SVKCMD
 CALL MAKFCB
 CP ","
 JP NZ,CMDERR
 INC HL
 CALL SETIMG
 CALL GET4H
 LD (SA),DE
 CP ","
 JP NZ,CMDERR
 CALL GET4H
 LD (EA),DE
 OR A
 LD DE,0
 JR Z,SVI10
 CALL GET4H
 PUSH HL
 LD HL,(SA)
 OR A
 EX DE,HL
 SBC HL,DE
 EX DE,HL
 POP HL
SVI10:
 LD (OF),DE
 LD (IX+12),1
 CALL SYSSET
 CALL WROPEN
 LD DE,(SA)
 LD HL,(EA)
 OR A
 SBC HL,DE
 INC HL
 LD C,L
 LD B,H
 LD HL,(SA)
 LD DE,(OF)
 ADD HL,DE
 LD A,(SA)
 CALL WRCHR
 LD A,(SA+1)
 CALL WRCHR
 PUSH HL
 LD HL,(EA)
 INC HL
 LD A,L
 CALL WRCHR
 LD A,H
 CALL WRCHR
 POP HL
SVI20:LD A,(HL)
 INC HL
 CALL WRCHR
 DEC BC
 LD A,C
 OR B
 JR NZ,SVI20
 CALL WRCLOS
 JP DOSCMD
;
LDICMD:
 LD IX,CFCB
 CP "K"
 JP Z,LDKCMD
 CALL MAKFCB
 CALL SETIMG
 INC HL
 CP ","
 LD DE,$FFFF
 CALL Z,GET4H
 LD (OF),DE
 LD (IX+12),1
 CALL SYSSET
 CALL RDOPENE
 CALL RDCHR
 LD L,A
 CALL RDCHR
 LD H,A
 LD (SA),HL
 PUSH HL
 CALL RDCHR
 LD L,A
 CALL RDCHR
 LD H,A
 DEC HL
 LD (EA),HL
 POP DE
 OR A
 SBC HL,DE
 INC HL
 LD C,L
 LD B,H
 LD HL,(OF)
 LD A,L
 AND H
 INC A
 JR NZ,LDI20
 EX DE,HL
LDI20:CALL RDCHR
 LD (HL),A
 INC HL
 DEC BC
 LD A,C
 OR B
 JR NZ,LDI20
 CALL RDCLOS
 JP DOSCMD
;
SVKCMD:
 CALL MAKFCB
 CALL SETFKY
 CALL SYSSET
 CALL WROPEN
 LD HL,FKBUF
 LD B,160
SVK10:LD A,(HL)
 INC HL
 PUSH BC
 CALL WRCHR
 POP BC
 DJNZ SVK10
 LD A,$1A
 CALL WRCHR
 CALL WRCLOS
 JP DOSCMD
;
LDKCMD:
 CALL MAKFCB
 CALL SETFKY
 CALL SYSSET
 CALL RDOPENE
 LD B,160
 LD HL,FKBUF
LDK10:CALL RDCHR
 LD (HL),A
 INC HL
 DJNZ LDK10
 DEC HL
 LD (HL),0
 CALL RDCLOS
 LD A,1
 LD (SHFTINF),A
 JP DOSCMD
;
MAKFCB:
 PUSH DE
 PUSH BC
 CALL MKFCB0
 POP BC
 POP DE
 RET
;
MKFCB0:
 PUSH IX
 POP DE
 CALL CLRFCB
MKF05:LD A,(HL)
 INC HL
 CP " "
 JR Z,MKF05
 CP 9
 JR Z,MKF05
 LD A,(HL)
 DEC HL
 CP ":"
 JR NZ,MKF10
 LD A,(HL)
 CALL UPPER
 CP "L"
 JR Z,MKFLPT
 CP "M"
 JR Z,MKFMEM
 CP "C"
 JR NZ,MKF06
 LD A,$F0
 DEFB $11
MKFLPT:LD A,$E0
 DEFB $11
MKFMEM:LD A,$D0
 PUSH IX
 POP DE
 JR MKF07
MKF06:SUB $31
 CALL TSTDRV
 JP C,IFNERR
 INC A
MKF07:LD (DE),A
 INC HL
 INC HL
MKF10:INC DE
 LD B,8
MKF20:LD A,(HL)
 CP " "+1
 RET C
 CP ","
 RET Z
 INC HL
 CP "."
 JR Z,MKF30
 LD (DE),A
 INC DE
 DJNZ MKF20
MKF30:
 PUSH HL
 PUSH IX
 POP HL
 LD DE,9
 ADD HL,DE
 EX DE,HL
 POP HL
 LD B,3
MKF40:LD A,(HL)
 CP " "+1
 RET C
 CP ","
 RET Z
 INC HL
 LD (DE),A
 INC DE
 DJNZ MKF40
 LD A,(HL)
 RET
;
CLRFCB:
 PUSH HL
 PUSH DE
 PUSH BC
 PUSH AF
 PUSH IX
 POP DE
 LD L,E
 LD H,D
 INC DE
 PUSH DE
 LD (HL),0
 LD BC,36-1
 LDIR
 POP DE
 LD L,E
 LD H,D
 LD (HL)," "
 INC DE
 LD BC,11-1
 LDIR
 POP AF
 POP BC
 POP DE
 POP HL
 RET
;
RDOPENE:CALL RDOPEN
 JP C,FNFERR
 RET
;"IX=[FCB]
RDOPEN:
 PUSH IY
 PUSH HL
 PUSH DE
 PUSH BC
;
 PUSH AF
 LD A,(IX+0)
 CP $F0
 JR Z,ROPEEE
 CP $E0
 JP Z,ILLMOD
 CP $D0
 JR Z,ROPMEM
 POP AF
;
 CALL RESDRV
 CALL SCHDIR
 JR C,ROPEER
 CALL TOFCB
 LD A,(IX+MODE)
 AND 3
 CP 2
 JR Z,ROPEE
 CALL RDBLK
ROPEE:POP BC
 POP DE
 POP HL
 POP IY
 OR A
 RET
ROPEER:
 POP BC
 POP DE
 POP HL
 POP IY
 SCF
 RET
;
ROPEEE:POP AF
 OR A
 JR ROPEE
;
ROPMEM:
 LD HL,($FF60)
 LD (IX+BFPL),L
 LD (IX+BFPH),H
 XOR A
 LD (RDMFFF),A
 JR ROPEEE
;
RDBLK:
 CALL SETDRV
 CALL NXTSEC
 CALL RDSEC
 LD A,(IX+SCC)
 CP 8
 JR NZ,RDBOK
 XOR A
 LD (IX+SCC),A
 LD A,(IX+NCL)
 CALL FATAD
 LD E,A
 LD D,0
 ADD HL,DE
 LD A,(HL)
 LD (IX+NCL),A
 AND $F0
 CP $C0
 JR NZ,RDBOK
 LD A,(IX+NCL)
 AND $0F
 LD (IX+BFCH),A
RDBNG:SCF
 RET
RDBOK:OR A
 RET
;
RDMEM:
 PUSH HL
 LD A,(RDMFFF)
 OR A
 LD A,0
 LD (RDMFFF),A
 LD A,10
 JR NZ,RDMEEE
 LD L,(IX+BFPL)
 LD H,(IX+BFPH)
 LD A,(HL)
 OR A
 JR Z,RDMEM10
 INC HL
 LD (IX+BFPL),L
 LD (IX+BFPH),H
 CP 13
 JR NZ,RDMEEE
 LD (RDMFFF),A
 JR RDMEEE
RDMEM10:LD A,$1A
 SCF
RDMEEE:POP HL
 RET
;
RDCHR:
 LD A,(IX+0)
 CP $F0
 JP Z,CONIN
 CP $E0
 JP Z,ILLMOD
 CP $D0
 JP Z,RDMEM
 PUSH HL
 PUSH DE
 PUSH BC
 CALL RDCHR0
 OR A
RDCHEC:POP BC
 POP DE
 POP HL
 RET
;
RDCHREE:
 POP AF
 LD A,$1A
 SCF
 JR RDCHEC
;
RDCHR0:
 LD L,(IX+BFCL)
 LD H,(IX+BFCH)
 LD A,L
 OR H
 JR NZ,RDC10
 LD A,(IX+NCL)
 AND $F0
 CP $C0
 JR Z,RDCHREE
 CALL RDBLK
 JR RDCHR
RDC10:
 DEC HL
 LD (IX+BFCL),L
 LD (IX+BFCH),H
 EX DE,HL
 LD L,(IX+BFPL)
 LD H,(IX+BFPH)
 LD B,(HL)
 INC HL
 LD (IX+BFPL),L
 LD (IX+BFPH),H
 LD A,E
 OR D
 LD A,B
 RET NZ
 LD A,(IX+NCL)
 AND $F0
 SUB $C0
 AND A
 LD A,B
 RET NZ
 SCF
 RET
;
RDCLOS:RET
;
;"IX=[FCB]
WROPEN:
 PUSH IY
 PUSH HL
 PUSH DE
 PUSH BC
 PUSH AF
;
 LD A,(IX+0)
 CP $F0
 JP Z,WOPEEE
 CP $E0
 JP Z,WOPEEE
 CP $D0
 JP Z,ILLMOD
;
 LD A,(IX+ATRB)
 LD (ATRKEEP),A
;
 CALL RESDRV
 CALL SCHDIR
 PUSH HL
 POP IY
 JR C,WOP10
 LD A,(IY+9)
 AND $10
 JP NZ,PRTERR
 CALL ERASEF
 JR WOP20
WOP10:
 CALL SCHFRE
 JP C,DIRFUL
 CALL TODIR
WOP20:
 PUSH HL
 POP IY
 CALL FRECLS
 LD A,(ATRKEEP)
 LD (IY+9),A
 LD (IY+10),B
 CALL RESTDIR
 CALL FFLGAD
 INC (HL)
 LD L,(IX+FPTL)
 LD H,(IX+FPTH)
 LD (HL),$C0
 LD (IX+NCL),B
 LD (IX+DRC),C
 LD (IX+SCC),0
 CALL NSCCOM
 CALL WRFAT
WOPEEE:POP AF
 POP BC
 POP DE
 POP HL
 POP IY
 RET
;
WRBLKS:
 LD A,(IX+SCC)
 CP 8
 JR NZ,WRBLK2
 XOR A
 LD (IX+SCC),A
WRBLK2:
 CALL SETDRV
 CALL NXTSEC
 CALL WRSEC
 RET
;
WRBLK1:
 CALL WRBLKS
 LD A,(IX+SCC)
 CP 8
 RET NZ
 CALL NXTFRE
 PUSH HL
 LD L,(IX+FPTL)
 LD H,(IX+FPTH)
 LD (HL),B
 POP HL
 LD (IX+FPTL),L
 LD (IX+FPTH),H
 LD (HL),$C0
 LD A,B
 LD (IX+NCL),A
 RET
;
NXTFRE:
 LD B,(IX+NCL)
 LD C,(IX+DRC)
 CALL TRYFRE
 RET NC
 LD B,(IX+NCL)
 LD C,(IX+DRC)
 LD A,C
 NEG
 LD C,A
 CALL TRYFRE
 RET NC
 JP DSKFUL
;
;
;"outputs are
;"B: cluster No.
;"HL: adrs of FAT[B]
TRYFRE:
 CALL FATAD
 PUSH DE
 LD E,B
 LD D,0
 ADD HL,DE
 POP DE
TRYAGN:
 PUSH DE
 PUSH HL
 LD L,C
 CALL SEX
 POP DE
 ADD HL,DE
 POP DE
 LD A,B
 ADD A,C
 LD B,A
 CP 160
 CCF
 RET C
 LD A,(HL)
 INC A
 RET Z
 JR TRYAGN
;
WRCHRC:POP AF
 PUSH AF
 CALL CONOUT
 CALL STPESC
 JR WRC10
WRPRINT:POP AF
 PUSH AF
 CALL LPTOUT
 CALL STPESC
 JR WRC10
;
;"IX=[FCB]
WRCHR:
 PUSH HL
 PUSH DE
 PUSH BC
 PUSH AF
 LD A,(IX+0)
 CP $F0
 JR Z,WRCHRC
 PUSH IX
 POP DE
 LD HL,(COFCB)
 POP AF
 PUSH AF
 OR A
 SBC HL,DE
 CALL Z,CONOUT
 LD A,(IX+0)
 CP $E0
 JP Z,WRPRINT
 CP $D0
 JP Z,ILLMOD
 POP AF
 PUSH AF
 LD L,(IX+BFPL)
 LD H,(IX+BFPH)
 LD (HL),A
 INC HL
 LD (IX+BFPL),L
 LD (IX+BFPH),H
 LD L,(IX+BFCL)
 LD H,(IX+BFCH)
 DEC HL
 LD (IX+BFCL),L
 LD (IX+BFCH),H
 LD A,L
 OR H
 JR NZ,WRC10
 CALL WRBLK1
WRC10:POP AF
 POP BC
 POP DE
 POP HL
 RET
;
WRCLOS:
 PUSH HL
 PUSH DE
 PUSH BC
 PUSH AF
 LD A,(IX+0)
 CP $F0
 JR Z,WRCL30
 CP $E0
 JR Z,WRCL30
 CP $D0
 JP Z,ILLMOD
 LD A,(IX+SCC)
 LD A,(IX+BFCL)
 OR A
 JR NZ,WRCL10
 LD A,(IX+BFCH)
 CP (IX+BFS)
 JR Z,WRCL20
WRCL10:
 LD L,(IX+BFPL)
 LD H,(IX+BFPH)
 PUSH HL
 LD C,(IX+BFCL)
 LD B,(IX+BFCH)
WRCL12:
 LD (HL),0
 INC HL
 DEC BC
 LD A,C
 OR B
 JR NZ,WRCL12
 POP HL
 LD E,(IX+BFTL)
 LD D,(IX+BFTH)
 OR A
 SBC HL,DE
 DEC HL
 INC H
 LD (IX+BFS),H
 CALL WRBLKS
WRCL20:LD L,(IX+FPTL)
 LD H,(IX+FPTH)
 LD A,(IX+SCC)
 ADD A,$C0
 LD (HL),A
 CALL SETDRV
 CALL WRFAT
 CALL FFLGAD
 DEC (HL)
WRCL30:POP AF
 POP BC
 POP DE
 POP HL
 RET
;
;"IX=[FCB](drv No.)
RESDRV:
 PUSH AF
 CALL SETDRV
 POP AF
;"[DRVNO]=drv No.
SCHRES:
 PUSH AF
 PUSH BC
 PUSH DE
 PUSH HL
 CALL RDFAT
 LD DE,(SYSBUF)
 LD B,18*2+1
 LD A,1
 LD (DIRSEC),A
 LD C,A
 LD A,8
 LD (DIRNUM),A
 CALL RDSEC
 LD HL,(SYSBUF)
 LD (IX+BFPL),L
 LD (IX+BFPH),H
 XOR A
 LD (DIRCNT),A
 POP HL
 POP DE
 POP BC
 POP AF
 RET
;
SCHFRE:
 PUSH DE
 PUSH BC
 CALL SCHRES
SRS10:
 LD A,(DIRCNT)
 CP 192
 JR Z,SRSERR
 CALL DIRSTP
 LD A,(HL)
 OR A
 JR Z,SRSEE
 CP $FF
 JR NZ,SRS10
SRSEE:POP BC
 POP DE
 OR A
 RET
SRSERR:POP BC
 POP DE
 SCF
 RET
;
RELODDIR:
 PUSH DE
 PUSH BC
 PUSH AF
 LD B,18*2+1
 LD A,(DIRSEC)
 LD C,A
 LD A,(DIRNUM)
 LD DE,(SYSBUF)
 CALL RDSEC
 POP AF
 POP BC
 POP DE
 RET
;
RESTDIR:
 PUSH DE
 PUSH BC
 PUSH AF
 LD B,18*2+1
 LD A,(DIRSEC)
 LD C,A
 LD A,(DIRNUM)
 LD DE,(SYSBUF)
 CALL WRSEC
 POP AF
 POP BC
 POP DE
 RET
;
;"in
;"IX=[FCB]
;"out
;"HL=[DIR]
SCHDIR:PUSH DE
 CALL NXTDIR
 POP DE
 RET C
 PUSH IY
 PUSH DE
 PUSH HL
 PUSH HL
 POP IY
 PUSH IX
 POP DE
 INC DE
 LD B,6
SDR10:LD A,(DE)
 INC DE
 CP "?"
 JR Z,SDR20
 CP (HL)
 JR NZ,SDRERR
SDR20:INC HL
 DJNZ SDR10
 INC DE
 INC DE
 LD B,3
SDR30:LD A,(DE)
 INC DE
 CP "?"
 JR Z,SDR40
 CP (HL)
 JR NZ,SDRERR
SDR40:INC HL
 DJNZ SDR30
 LD A,(IY+9)
 LD (IX+ATRB),A
 LD A,(IY+10)
 LD (IX+NCL),A
 LD (IX+FCL),A
 POP HL
 POP DE
 POP IY
 OR A
 RET
;
SDRERR:POP HL
 POP DE
 POP IY
 JR SCHDIR
;
NXTDIR:
 LD A,(DIRCNT)
 CP 192
 JR Z,SDIERR
 CALL DIRSTP
 LD A,(HL)
 CP $FF
 JR Z,SDIERR
 OR A
 JR Z,NXTDIR
 RET
;
SDIERR:
SCFRET:
 SCF
 RET
;
DIRSTP:
 LD A,(DIRCNT)
 CP 128
 JR NZ,SDI10
 LD DE,(SYSBUF)
 LD B,18*2+1
 LD A,9
 LD (DIRSEC),A
 LD C,A
 LD A,4
 LD (DIRNUM),A
 CALL RDSEC
 LD HL,(SYSBUF)
 LD (IX+BFPL),L
 LD (IX+BFPH),H
SDI10:
 LD L,(IX+BFPL)
 LD H,(IX+BFPH)
 PUSH HL
 LD DE,16
 ADD HL,DE
 LD (IX+BFPL),L
 LD (IX+BFPH),H
 LD A,(DIRCNT)
 INC A
 LD (DIRCNT),A
 POP HL
 RET
;
;"HL=[DIR]
ERASEF:
 PUSH HL
 PUSH DE
 PUSH AF
 LD DE,10
 ADD HL,DE
 LD E,(HL)
ERS10:LD D,0
 CALL FATAD
 ADD HL,DE
 LD A,(HL)
 PUSH AF
 LD (HL),$FF ;FREE
 POP AF
 LD E,A
 AND $F0
 CP $C0
 JR NZ,ERS10
 POP AF
 POP DE
 POP HL
 RET

;
;"output
;B=CLUSTER NUM
;C=DIRECTION
FRECLS:
 PUSH HL
 PUSH DE
 CALL FATAD
 LD DE,37*2 ;(18*2+1)*2
 LD B,E
 ADD HL,DE
 PUSH HL
 PUSH BC
 LD C,1
 EXX 
 POP BC
 LD C,$FF
 POP HL
FRC10:
 EXX
 INC HL
 INC B
 LD A,B
 CP 80*2
 JP Z,DSKFUL
 LD A,(HL)
 INC A
 JR Z,FRCFND
 EXX
 DEC HL
 LD A,B
 OR A
 JR Z,FRC10
 DEC B
 LD A,(HL)
 INC A
 JR NZ,FRC10
FRCFND:
 LD (IX+FPTL),L
 LD (IX+FPTH),H
 LD A,B
 POP DE
 POP HL
 RET
;
NXTSEC:
 LD A,(IX+NCL)
 LD B,A
 SRL B
 LD C,1
 AND 1
 JR Z,NSC10
 LD C,9
NSC10:LD A,C
 ADD A,(IX+SCC)
 LD C,A
 LD A,(IX+SCC)
 ADD A,(IX+BFS)
 LD (IX+SCC),A
NSCCOM:
 LD (IX+BFCL),0
 LD A,(IX+BFS)
 LD (IX+BFCH),A
 LD E,(IX+BFTL)
 LD D,(IX+BFTH)
 LD (IX+BFPL),E
 LD (IX+BFPH),D
 RET
;
SYSSET:
 LD HL,(SYSBUF)
 LD BC,(SYSBFS-1) ;AS LD B,(SYSBFS)
FCBSET:
 LD (IX+MODE),A
 LD (IX+BFTL),L
 LD (IX+BFTH),H
 LD (IX+BFS),B
 LD (IX+SCC),0
 JR NSCCOM
;
CHKTYP:
 PUSH IX
 PUSH HL
 LD B,3
CHK10:LD A,(HL)
 INC HL
 CP (IX+9)
 INC IX
 JR NZ,CHKEE
 DJNZ CHK10
 POP HL
 POP IX
 OR A
 RET
CHKEE:POP HL
 POP IX
 SCF
 RET
;
KEEPDP:
 PUSH HL
 PUSH DE
 PUSH BC
 LD HL,DIRCNT
 LD DE,KPDP
 LD BC,3
 LDIR
 POP BC
 POP DE
 POP HL
 RET
RELODDP:
 PUSH HL
 PUSH DE
 PUSH BC
 LD DE,DIRCNT
 LD HL,KPDP
 LD BC,3
 LDIR
 POP BC
 POP DE
 POP HL
 RET
;
RDFAT:
 PUSH HL
 PUSH DE
 PUSH BC
 PUSH AF
 CALL FFLGAD
 LD A,(HL)
 OR A
 JP NZ,RDF10
 LD B,18*2+1
 LD C,14
 EX DE,HL
 CALL FATAD
 EX DE,HL
 LD A,1
 CALL RDSEC
RDF10:POP AF
 POP BC
 POP DE
 POP HL
 RET
;
WRFAT:
 PUSH HL
 PUSH DE
 PUSH BC
 PUSH AF
 LD B,18*2+1
 LD C,14
 EX DE,HL
 CALL FATAD
 EX DE,HL
WRF10:LD A,1
 CALL WRSEC
 INC C
 LD A,C
 CP 17
 JR NZ,WRF10
 POP AF
 POP BC
 POP DE
 POP HL
 RET
;
FATAD:
 PUSH DE
 PUSH AF
 LD A,(DRVNO)
 LD H,A
 LD L,0
 LD DE,FATBFS
 ADD HL,DE
 POP AF
 POP DE
 RET
;
FFLGAD:LD A,(DRVNO)
 PUSH DE
 LD E,A
 LD D,0
 LD HL,FATFLG
 ADD HL,DE
 POP DE
 RET
;
SETDRV:
 LD A,(IX+DVS)
 CP $F0
 RET Z
 CP $E0
 RET Z
 CP $D0
 RET Z
 OR A
 JR Z,SDV10
 DEC A
SDV10:LD (DRVNO),A
 RET
;
;[IX]=FCB
;[HL]=DIRECTORY
TODIR:
 PUSH HL
 PUSH DE
 PUSH BC
 PUSH IX
 POP DE
 EX DE,HL
 INC HL
 LD BC,6
 LDIR
 INC HL
 INC HL
 LD BC,3
 LDIR
 POP BC
 POP DE
 POP HL
 RET
;
;[IX]=FCB
;[HL]=DIRECTORY
TOFCB:
 PUSH HL
 PUSH DE
 PUSH BC
 PUSH IX
 POP DE
 INC DE
 LD BC,6
 LDIR
 INC DE
 INC DE
 LD BC,3
 LDIR
 POP BC
 POP DE
 POP HL
 RET
;
TSTDRV:
 CP $F0
 RET Z
 CP $E0
 RET Z
 CP $D0
 RET Z
 PUSH AF
 LD A,(DRIVES)
 LD B,A
 POP AF
 CP B
 CCF
 RET C
 CP 2+1
 CCF
 RET
;
GET4H:
 EX DE,HL
 LD HL,0
GET4H1:LD A,(DE)
 INC DE
 CALL UPPER
 CALL TSTHEX
 EX DE,HL
 RET C
 EX DE,HL
 CALL ADDHEX
 JR GET4H1
;
ADDHEX:
 CALL TST09
 JR NC,ADHX10
 SUB "A"-10
 JR ADHX20
ADHX10:SUB "0"
ADHX20:PUSH DE
 ADD HL,HL
 ADD HL,HL
 ADD HL,HL
 ADD HL,HL
 LD E,A
 LD D,0
 ADD HL,DE
 POP DE
 RET
;
TSTHEX:
 CALL TST09
 RET NC
 CALL TSTAF
 RET
;
TST09:
 CP "0"
 RET C
 CP "9"+1
 CCF
 RET
;
TSTAF:
 CP "A"
 RET C
 CP "F"+1
 CCF
 RET
;
;"need to debug
GENHEX:
 AND $F
 ADD A,"0"
 CP "9"+1
 RET C
 ADD A,"A"-"0"-10
 RET
;
UPPER:
 CP "a"
 RET C
 CP "z"+1
 RET NC
 SUB $20
 RET
;
SEX:PUSH AF
 LD A,L
 RLA
 SBC A,A
 LD H,A
 POP AF
 RET
;
TSTFLC:
 LD A,(TPA)
 CP $C3
 RET NZ
 LD A,(TPA+1)
 OR A
 RET NZ
 LD A,(TPA+2)
 OR A
 RET
;
FLCCLOS:
 CALL TSTFLC
 RET NZ
 CALL TPA+$C
 LD A,0
 LD (TPA),A
 RET
;
ILLMOD:LD HL,IMDERM
 JR ERRCOM
FTNERR:LD HL,FTNERM
 JR ERRCOM
DIRFUL:LD HL,DRFERM
 JR ERRCOM
DSKFUL:LD HL,DFLERM
 JR ERRCOM
IFNERR:LD HL,IFNERM
 JR ERRCOM
IPEERR:LD HL,IPEERM
 JR ERRCOM
EXSERR:LD HL,EXSERM
 JR ERRCOM
FNFERR:LD HL,FNFERM
 JR ERRCOM
SRVERR:LD HL,SRVERM
 JR ERRCOM
PRTERR:LD HL,PRTERM
 JR ERRCOM
PHYERR:LD HL,PHYERM
 JR ERRCOM
;
ERRCOM:
 XOR A
 LD (ERRCNT),A
 CALL SRESET
 CALL PRNXT
 DEFB $D,$A,0
 CALL PRSTR
 CALL PRNXT
 DEFB " Error!",7,$D,$A,0
 JP START
;
IMDERM:DEFB "I/O mode",0
FTNERM:DEFB "File Attribute not Match",0
DRFERM:DEFB "Directory Full",0
DFLERM:DEFB "Disk full",0
IFNERM:DEFB "Illegal File Name",0
IPEERM:DEFB "Input past End",0
EXSERM:DEFB "File already exist",0
FNFERM:DEFB "File not Found",0
SRVERM:DEFB "SRV",0
PRTERM:DEFB "Protected",0
PHYERM:DEFB "Disk I/O",0
;
SECINF:PUSH HL
 PUSH DE
 PUSH BC
 PUSH AF
 CALL PRNXT
 DEFB "TRK:",0
 LD L,B
 LD H,0
 CALL PRDEC
 CALL PRNXT
 DEFB " SEC:",0
 LD L,C
 LD H,0
 CALL PRDEC
 CALL PRNXT
 DEFB " TRN:",0
 LD L,A
 LD H,0
 CALL PRDEC
 CALL PRCRLF
 POP AF
 POP BC
 POP DE
 POP HL
 RET
;
RDSEC:
; CALL SECINF
 PUSH BC
 PUSH DE
 PUSH AF
 LD (TRSECS),A
 LD A,2
 ROMCALL $121
 POP AF
 PUSH AF
 LD B,A
 CALL RSLTST
 AND $40
 JP Z,PHYERR
 LD A,$12
 CALL TRCMD
RDS10:LD C,$80
RDS15:LD A,5*2+1
 OUT ($FF),A
RDS20:IN A,($FE)
 AND 1
 JR Z,RDS20
 LD A,5*2
 OUT ($FF),A
 IN A,($FC)
 LD (DE),A
 INC DE
 LD A,6*2+1
 OUT ($FF),A
RDS30:IN A,($FE)
 AND 1
 JR NZ,RDS30
 IN A,($FC)
 LD (DE),A
 INC DE
 LD A,6*2
 OUT ($FF),A
 DEC C
 JR NZ,RDS15
 DJNZ RDS10
 CALL RSLTST
 POP AF
 POP DE
 POP BC
 RET
;
;
WRSEC:
 PUSH BC
 PUSH DE
 PUSH AF
 LD (TRSECS),A
 LD A,$14
 CALL TRCMD
 LD A,(DRVNO)
 CALL TR1B
 CALL RC1B
 AND $40
 JP NZ,PRTERR
 LD A,$11
 ROMCALL $121
 POP AF
 PUSH AF
 LD B,A
WRS10:LD C,$80
WRS20:IN A,($FE)
 AND 2
 JR Z,WRS20
 LD A,(DE)
 INC DE
 OUT ($FD),A
 LD A,4*2+1
 OUT ($FF),A
WRS30:IN A,($FE)
 AND 4
 JR Z,WRS30
 LD A,(DE)
 INC DE
 OUT ($FD),A
 LD A,4*2
 OUT ($FF),A
WRS40:IN A,($FE)
 AND 4
 JR NZ,WRS40
 DEC C
 JR NZ,WRS20
 DJNZ WRS10
 CALL RSLTST
 POP AF
 POP DE
 POP BC
 RET
;
RSLTST:
 LD A,6
 CALL TRCMD
 CALL RC1B
 AND $41
 CP 1
 RET NZ
 JP PHYERR
;
TRCMD:PUSH AF
 LD A,7*2+1
 OUT ($FF),A
 POP AF
TR1B:PUSH BC
 PUSH DE
 PUSH AF
TR1B10:IN A,($FE)
 AND 6
 CP 6
 JR Z,TR1B10
 AND 2
 JR Z,TR1B10
 LD A,7*2
 OUT ($FF),A
 POP AF
 PUSH AF
 OUT ($FD),A
 LD A,4*2+1
 OUT ($FF),A
TR1B40:IN A,($FE)
 AND 4
 JR Z,TR1B40
 LD A,4*2
 OUT ($FF),A
TR1B50:IN A,($FE)
 AND 4
 JR NZ,TR1B50
 POP AF
 POP DE
 POP BC
 RET
;
RC1B:ROMCALL $1E9
 RET
;
;"including Kernel-1
 INCLUDE "kern1.aza
;
SYSBFS:DEFB 2048/256
CIFCB:DEFW CINFCB
COFCB:DEFW COUTFCB
;
;"DOS working area
WK:DEFS 2
XWK:DEFS 2
;
SA:DEFS 2
EA:DEFS 2
OF:DEFS 2
;
DIRCNT:DEFS 1
DIRSEC:DEFS 1
DIRNUM:DEFS 1
KPDP:DEFS 3
;
RDMFFF:DEFS 1
ATRKEEP:DEFS 1
;
FATFLG:DEFS 4
;
CFCB:DEFS 36
CFCB2:DEFS 36
CINFCB:DEFS 36
COUTFCB:DEFS 36
;
REDCINBUF:DEFS 256
REDCOUTBUF:DEFS 256
FATBFS:DEFS 256*2 ;MAX DRIVES 2
;
ZZZZZZ:
.DEPHASE
 END
; 
