;
; ACCESS PC-8801 GV-RAM  FOR AZA V3.3
;                       (USED WHEN BUF88 EQU TRUE)
;
;
; WHEN (PC88FLG) THEN 88 ELSE OTHER MACHINE
TEST88:	XOR	A
	LD	(PC88FLG),A
	SRV	ALOMAP
	LD	A,(IY+2)	;MACHINE BYTE
	AND	%0000$0111
	CP	%0000$0110
	RET	NZ
	XOR	A
	LD	(OUTBUFFLG),A
	LD	A,1
	LD	(OUTREADFLG),A
	LD	(PC88FLG),A
	LD	HL,$C000
	LD	(GRMTOP),HL
	LD	(GRMPTR),HL
	LD	A,$5C
	LD	(BANK_SEL),A
	LD	A,3
	LD	(BANKS),A
;
	SRV	CPRNXT
	DB	"MACHINE is PC8801,BUFFERING SOURCE on GV-RAM"
	DB	$D,$A,0
;
	RET
;
; SET GVRAM A CLUSTER
;       INPUT (BUFA):BUFFER ADRS
SETGRM:	
	LD	A,(OUTBUFFLG)
	OR	A
	RET	NZ
;
	LD	HL,(BUFA)
	LD	DE,(GRMPTR)
	LD	A,(BANK_SEL)
	LD	C,A
;
	DI			;GVRAM ACCESS
	OUT	(C),A
	LD	BC,8*256
	LDIR
	OUT	($5F),A
	EI
;
	LD	(GRMPTR),DE
	LD	A,D
	OR	A
	RET	NZ
;
	LD	HL,(GRMTOP)
	LD	(GRMPTR),HL
	LD	A,(BANK_SEL)
	INC	A
	LD	(BANK_SEL),A
	LD	A,(BANKS)
	DEC	A
	LD	(BANKS),A
	RET	NZ
	LD	A,1
	LD	(OUTBUFFLG),A
	RET
;
; RESTORE FUNCTION FOR READVRM(WHEN PASS-2)
RESTORE_GRM:	
	XOR	A
	LD	(OUTREADFLG),A
	LD	HL,(GRMTOP)
	LD	(GRMPTR),HL
	LD	A,$5C
	LD	(BANK_SEL),A
	LD	A,3
	LD	(BANKS),A
;
	RET
;
;
;
;
READGRM:	
	LD	DE,(BUFA)
	LD	HL,(GRMPTR)
	LD	A,(BANK_SEL)
	LD	C,A
;
	DI			;GVRAM ACCESS
	OUT	(C),A
	LD	BC,8*256
	LDIR
	OUT	($5F),A
	EI
;
	LD	(GRMPTR),HL
	LD	A,H
	OR	A
	RET	NZ
;
	LD	HL,(GRMTOP)
	LD	(GRMPTR),HL
	LD	A,(BANK_SEL)
	INC	A
	LD	(BANK_SEL),A
	LD	A,(BANKS)
	DEC	A
	LD	(BANKS),A
	RET	NZ
	LD	A,1
	LD	(OUTREADFLG),A
	RET
;
;
;
PC88FLG:	DB	0
;
GRMTOP:	DW	0
GRMPTR:	DW	0
BANK_SEL:	DB	0
BANKS:	DB	0
OUTBUFFLG:	DB	0
OUTREADFLG:	DB	0
;
; END OF INCLUDED MODULE 'gr.aza'
