;
;PART OF LABEL SORTer
;   FOR AZA V3.0
;DEC.1984
;
	IF	FILEVER
LABEL_SORT:
;SET UP FOR SORT
        SRV     CPRNXT
	DEFB	"Sorting Labels...",$D,$A,0
;
	CALL	UNFREZ
	LD	BC,0
	LD	HL,(WRKTOP)
	LD	DE,WSRIDXS
	ADD	HL,DE
	LD	DE,SORTBUF	;USING SORCE INPUT BUF.
LSU100:	KEEP	DE
	EX	DE,HL
	LD	HL,(APOINT)
	OR	A
	SBC	HL,DE
	EX	DE,HL
	KEEPOUT
	JR	Z,LSUEEE
	KEEP	HL
	INC	HL+
	LD	A,(ATTFLG)
	OR	A
	JR	NZ,LSU190
	LD	A,(HL)
	CP	"@"
LSU190:	KEEP	AF
LSU200:	LD	A,(HL+)
        OR      A
	JP	P,LSU200
	KEEPOUT
	LD	A,(HL+++)
	EX	(SP),HL
	JR	Z,LSU300
	CP	$F1
	JR	NZ,LSU300
	LD	A,B
	CP	2048/256
	JP	NC,TMLERR
;
	EX	DE,HL
	LD	(HL+),E
	LD	(HL+),D
	EX	DE,HL
	INC	BC
LSU300:	KEEPOUT
	JR	LSU100
LSUEEE:	LD	(NUM_LABELS),BC
;
;
;
;SHELL SORT
	LD	HL,(NUM_LABELS)	;GAP:=NUM_LABELS
	LD	(GAP),HL
L1050:	LD	HL,(GAP)
	SRL	H		;GAP:=int(GAP/2)
	RR	L
	LD	A,L		;IF GAP=0 THEN EXIT
	OR	H
	JR	Z,SRTEEE
	LD	(GAP),HL
	EX	DE,HL		;MAX:=NUM_LABELS-GAP
	LD	HL,(NUM_LABELS)
	OR	A
	SBC	HL,DE
	LD	(MAX),HL
L1080:	LD	C,0		;FLAG:=FALSE
	LD	HL,0		;LOOPCNT:=0
	LD	(LOOPCNT),HL
SRTLOOP:	
	LD	HL,(LOOPCNT)	;DST:=LOOPCNT+GAP
	LD	DE,(GAP)
	ADD	HL,DE
	LD	(DST),HL
	CALL	CMPLBL		;IF M(LOOPCNT)>M(DST)
	JR	Z,SRTNOT	;THEN SWAP ; FALG:=TRUE
	CALL	SWAPLBL
        LD      C,1
SRTNOT:
        LD      HL,(LOOPCNT)
	INC	HL
	LD	(LOOPCNT),HL
	LD	DE,(MAX)
	OR	A
	SBC	HL,DE
	JR	NZ,SRTLOOP
	LD	A,C
        OR      A
	JR	NZ,L1080
        JR      L1050
;
SRTEEE:	RET
;
;
CALCSI:	ADD	HL,HL
	LD	DE,SORTBUF
	ADD	HL,DE
	RET
;
INDHL:	KEEP	DE
	LD	E,(HL+)
	LD	D,(HL)
	EX	DE,HL
	KEEPOUT
	RET
;
SWAPLBL:	
	LD	HL,(SPTR1)
	LD	DE,(SPTR2)
	CALL	SWPBIT
	INC	HL
	INC	DE
SWPBIT:	LD	B,(HL)
	LD	A,(DE)
	LD	(HL),A
	LD	A,B
	LD	(DE),A
	RET
;
;
SPTR1:	DEFS	2
SPTR2:	DEFS	2
DST:	DEFS	2
GAP:	DEFS	2
MAX:	DEFS	2
LOOPCNT:	DEFS	2
NUM_LABELS:	DEFS	2
	ENDIF
;
;"print out label table
PUTLBL:	
LABELS: IF      FILEVER
LABELS_EX:	
        CALL    NEWPAGE
	ENDIF
	LD	IX,LSTFCB
	CALL	UNFREZ
	SRV	PUTNXT
	DEFB	$D,$A,"Label List",$D,$A,0
	IF	FILEVER
	LD	A,4+1
	LD	(PGCOUNT),A
	LD	IY,SORTBUF
	ELSE
	LD	IY,(WRKTOP)
	LD	DE,WSRIDXS
	ADD	IY,DE
        ENDIF
	LD	BC,0
	EXX
LBS50:	SRV	PUTCRLF
	IF	FILEVER
	LD	A,(PGCOUNT)
	LD	B,A
	LD	A,(PAGES)
	SUB	3+1
	CP	B
	JP	NC,LBS60
	XOR	A
	LD	(PGCOUNT),A
	CALL	NEWPAGE
LBS60:	LD	A,(PGCOUNT)
	INC	A
	LD	(PGCOUNT),A
	ENDIF
LBS90:	IF	FILEVER
	EXX
        KEEP    HL
        LD      HL,(NUM_LABELS)
	OR	A
	SBC	HL,BC
	KEEPOUT
	EXX
	JR	Z,LBSEEE
        ELSE
	LD	DE,(APOINT)
	PUSH	IY
	POP	HL
	OR	A
	SBC	HL,DE
	JP	Z,LBSEEE
	INC	IY
	INC	IY
	ENDIF
        SRV     STPESC
	LD	HL,LSTBUF
	PUSH	HL
	LD	B,19-6
	PUSH	BC
	LD	A,"€"
LBS200:	LD	(HL+),A
	DJNZ	LBS200
	LD	(HL),0
	POP	BC
	POP	HL
	PUSH	HL
	IF	FILEVER
	KEEP	DE
	LD	E,(IY+0)
	LD	D,(IY+1)
	INC	IY
	INC	IY
	INC	DE
	INC	DE
LBS300: LD      A,(DE+)
        KEEP    AF
	AND	$7F
	LD	(HL+),A
        KEEPOUT
	OR	A
	JP	M,LBS390
	DJNZ	LBS300
LBS350:	LD	A,(DE+)
	OR	A
	JP	P,LBS350
LBS390:	INC	DE
	EX	DE,HL
	LD	E,(HL+)
	LD	D,(HL)
	EX	DE,HL
	LD	(VALUE),HL
        KEEPOUT
	ELSE
LBS300: LD      A,(IY+0)
	INC	IY
	PUSH	AF
	AND	$7F
	LD	(HL+),A
	POP	AF
	OR	A
	JP	M,LBS400
	DJNZ	LBS300
LBS350:	LD	A,(IY+0)
	INC	IY
	OR	A
	JP	P,LBS350
	ENDIF
LBS400: POP     HL
;!!!!!!!!!!!!!!!!
	LD	HL,LSTBUF
        IF      FILEVER
	ELSE
        INC     IY
	INC	IY
	INC	IY
        LD      A,(IY-3)
	CP	$F1
	JR	NZ,LBS90
	LD	A,(LSTBUF)
	CP	"@"
	JR	Z,LBS90
	ENDIF
        SRV     PUTSTR
	LD	A,":"
	SRV	PUTCHR
	IF	FILEVER
	LD	HL,(VALUE)
	ELSE
        LD      L,(IY-2)
	LD	H,(IY-1)
	ENDIF
        SRV     PUT4HEX
	EXX
	INC	BC
	LD	A,C
	EXX
	AND	3
	JP	Z,LBS50
	LD	A,","
	SRV	PUTCHR
	JP	LBS90
;
LBSEEE:
	SRV	PUTNXT
	DEFB	$D,$A,"Total Labels: ",0
	EXX
	LD	L,C
	LD	H,B
	SRV	PUTDEC
	SRV	PUTCRLF
	SRV	PUTCRLF
	RET
;
;PUT OUT .sym FILE
	IF	FILEVER
PUTSYM:	LD	IX,SYMFCB
	LD	IY,OBJFCB
	CALL	COPYFN
	LD	(IX+9),"s"
	LD	(IX+10),"y"
	LD	(IX+11),"m"
	LD	HL,FSYMBUF
	LD	B,1
	LD	A,1		;OUTPUT SEQ.
	SRV	OPEN
	CALL	UNFREZ
	LD	IY,SORTBUF
	LD	BC,0
	EXX
PSY50:	LD	B,4
PSY60:	EXX
        KEEP    HL
        LD      HL,(NUM_LABELS)
	OR	A
	SBC	HL,BC
	KEEPOUT
	EXX
	JR	Z,PSYEEE
PSY90:	LD	E,(IY+0)
	LD	D,(IY+1)
	INC	IY
	INC	IY
	INC	DE
	INC	DE
	LD	A,(DE)
	CP	"@"
	JR	Z,PSY90
	KEEP	DE
PSY100:	LD	A,(DE+)
	OR	A
	JP	P,PSY100
	LD	A,(+DE)
	KEEP	AF
	LD	A,(+DE)
	SRV	PUT2HEX
	KEEPOUT
	SRV	PUT2HEX
	SRV	PUTSPC
	KEEPOUT
;        LD      C,6
PSY200:	LD	A,(DE+)
	KEEP	AF
	AND	$7F
	SRV	PUTCHR
	KEEPOUT
;        DEC     C
;        JR      Z,PSY220
	OR	A
	JP	P,PSY200
PSY220:	LD	A,9
	SRV	PUTCHR
	EXX
	INC	BC
	EXX
	DJNZ	PSY60
	SRV	PUTCRLF
	JR	PSY50
;
PSYEEE:	SRV	PUTCRLF
	LD	A,$1A
	SRV	PUTCHR
	SRV	CLOSE
	SRV	CPRCRLF
	RET
	ENDIF
;
;
PUTDNAM:	
	LD	A,(PASS)
	CP	1
	RET	Z
	LD	A,(XCRFFLG)
	OR	A
	RET	NZ
	LD	A,"#"
	LD	IX,CRFFCB
	SRV	PUTCHR
        JR      PUTNAMCOM
PUTNAM:	LD	A,(PASS)
	CP	1
	RET	Z
	LD	A,(XCRFFLG)
	OR	A
	RET	NZ
        LD      IX,CRFFCB
PUTNAMCOM:	
	LD	HL++,(APOINT)
	LD	A,(ATTFLG)
	OR	A
	JR	NZ,PTNM10
	LD	A,(HL)
	CP	"@"
	RET	Z
PTNM10:	LD	A,(HL+)
	KEEP	AF
	SRV	PUTCHR
	KEEPOUT
	OR	A
	JP	P,PTNM10
	RET
;
;
;
;
