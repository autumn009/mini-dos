	PAGE	120
;R10
;
;Aza  Aki's Zilog mnemonic Assembler
; under MINI-DOS
; by Aki Jul.1984 FOR VER 2.0
; MODEFYED Oct.1984 FOR VER 2.2
; MODEFYED Oct.1984 FOR VER 2.21 (EXPAND I.D. TABLE)
; MODEFYED Dec.1984 FOR VER 3.0 (HIGH SPEED VERSION)
; DEBUGED Jan.1985  FOR VER 3.01 (INDEX OFFSET)
; DEBUGED Feb.1985  FOR VER 3.02 (MANY)
; MODEFYED Feb.1985 FOR VER 3.1  (ALWAYS MAKE OBJ)
; MODEFYED May.1985 FOR VER 3.2 (MNEMONIC OK where LINE TOP)
; MODEFYED Sep.1985 FOR VER 3.3 (DEBUG & GRAM BUFFER ON 88)
; DEBUGED Oct.1985  FOR VER 3.31 (MANY)
; DEBUGED Oct.1985  FOR VER 3.32 (PP-FLG was CLEARED by EXPRES)
;
;---------------------
;
	TITLE	2:asn.trc
;
FALSE	EQU	0
TRUE	EQU	NOT FALSE
;
DEBUG	EQU	FALSE
FILEVER	EQU	TRUE
INDICATE	EQU	FALSE
HIGHSPEED	EQU	FALSE
VER8	EQU	FALSE
;
BUFSIZE	EQU	1		;AS 8*256*1 BYTES
;
	IF	FILEVER
PHASEAD	EQU	$100
	ELSE
PHASEAD	EQU	$120
	ENDIF
;
	ORG	PHASEAD
.PHASE	PHASEAD
;
	JP	START
	JP	START
	DEFB	"Aza"
	JP	START
	JP	0		;"dummy
	JP	LABEL
	JP	CLEAR
;
WRKTOP:	DEFW	LABELSTART
ENDARE:	DEFW	LABELEND
;
LABEL:	CALL	LABELS
	RST	$28
	DEFB	WARM
;
TEXT	EQU	$FF60
POINTR	EQU	$FF62
ERRMARK	EQU	$FF70
;
.XLIST
.XCRF
	INCLUDE	dosequ		;
	INCLUDE	ad		;DATA TABLE
.CRF
.LIST
	INCLUDE	gr		;PC88 GVRAM DRIVER
	INCLUDE	ae		;EXPANDED GIJI
	INCLUDE	acal		;CALC CONSTANT EXPRESSION
	INCLUDE	aa		;ANALIZE OPERAND
	INCLUDE	awsr		;WORD SEARCHER
	INCLUDE	ax		;LABEL PRINTER
	INCLUDE	al		;LABEL COMPAIR
;
START:	LD	SP,$FFF0
	IF	FILEVER
	SRV	CPRNXT
	DEFB	$D,$A,"AZA assembler version 3.32",$D,$A
	IF	HIGHSPEED
	DEFB	"SPECIAL",$D,$A
	ENDIF
	DEFB	"Copyright 1984 (c) by Akira Kita",$D,$A,$A,0
	ENDIF
	PUSH	HL
;
	CALL	TEST88
;
	SRV	ALOMAP
	LD	HL,0
	LD	(IY+6),L
	LD	(IY+7),H
	LD	L,(IY+10)	;UCA END is END OF ID-TABLE
	LD	H,(IY+11)
	LD	(ENDARE),HL
;
	LD	HL,(WRKTOP)
	LD	($FF66),HL
	LD	HL,(ENDARE)
	LD	($FF68),HL
	POP	HL
	XOR	A
	LD	(LNFLG),A
	LD	(LSTFLG),A
	LD	(LBLFLG),A
	LD	(SYMFLG),A
	LD	(NOBUF),A
	LD	(ATTFLG),A
	LD	(NIFFLG),A
	LD	(IMGMODE),A
	LD	(TWNFLG),A
	IF	FILEVER
	LD	(DEBFLG),A
	LD	(RUNFLG),A
	LD	(STOPFLG),A
	LD	(MOBJFLG),A
	ENDIF
	IF	FILEVER
	LD	IX,OBJFCB
	LD	(IX+0),A
	LD	B,11
STR001:	LD	(IX+1),$20
	INC	IX
	DJNZ	STR001
	ENDIF
	LD	A,1		;ALWAYS MAKE OBJECT
	LD	(OBJFLG),A
	LD	(NOBUF),A
	LD	A,$F0
	LD	(LSTFCB),A
;
	LD	IX,SRCFCB
	IF	FILEVER
	SRV	MAKFCB
	INC	HL
	PUSH	AF
	CALL	SETAZA
	POP	AF
	OR	A
	JP	Z,STR300
	ELSE
	LD	(IX+0),$D0
	ENDIF
;
STR100:	LD	A,(HL)
	INC	HL
	CALL	UPPER
	CP	" "
	JR	Z,STR100
	CP	","
	JR	Z,STR100
	CP	"/"
	JR	Z,STR100
	CP	"@"
	JP	Z,STRATT
        CP      "L"
	JR	Z,STR200
	CP	"Q"
	JP	Z,STR260
	CP	"N"
	JP	Z,STRNIF
	IF	FILEVER
	CP	"X"
	JP	Z,STR250
	CP	"Y"
	JP	Z,STRSYM
	CP	"F"
	JP	Z,STRFILE
	CP	"C"
	JP	Z,STRCRF
	CP	"A"
	JP	Z,STRLN
        ENDIF
	OR	A
	JP	Z,STR300
OPTERR:	SRV	CPRNXT
	DEFB	$D,$A,"Option Error!",7,$D,$A,0
	SRV	WARM
;
; Q option
STR260:	LD	B,0
	JR	STRLSTCOM
;
; L option
STR200:	LD	B,1
STRLSTCOM:	
	LD	IX,LSTFCB
	LD	A,(HL+)
	CP	"-"
	JP	Z,STR100
	CP	"+"
	JR	Z,STR201
	LD	A,B
	LD	(LSTFLG),A
	LD	A,1
	LD	(LBLFLG),A
	LD	(IX+0),$F0	;SET CONSOLE
	DEC	HL
	JP	STR100
STR201:	LD	A,B
	LD	(LSTFLG),A
	LD	A,1
	LD	(LBLFLG),A
	IF	FILEVER
	SRV	MAKFCB
	LD	B,(IX+1)
	LD	A,(IX+0)
	CP	$E0
	JR	NZ,STR202
	LD	(IX+1),"@"
	JP	STR100
STR202:	LD	A,B
	CP	" "
	JP	NZ,STR100
	ENDIF
	LD	(IX+0),$E0	;SET PRINTER
	JP	STR100
;
STRNIF:	
	LD	A,1
	LD	(NIFFLG),A
	JP	STR100
;
STRATT:	LD	A,1
	LD	(ATTFLG),A
	JP	STR100
;
	IF	FILEVER
;
STRFILE:	LD	B,1
STROBJCOM:	
	XOR	A
	LD	(OBJFLG),A
	LD	IX,OBJFCB
	LD	A,(HL+)
	CP	"-"
	JP	Z,STR100
	KEEP	AF
	LD	A,1
	LD	(OBJFLG),A
	LD	A,B
	LD	(NOBUF),A
	KEEPOUT
	OR	A
	JR	Z,STR243
	CP	"+"
	JR	Z,STR243
STR242:	DEC	HL
STR243:	LD	A,1
	LD	(MOBJFLG),A
	SRV	MAKFCB
	OR	A
	JP	Z,STR300
	JP	STR100
; X option
STR250:	LD	(STOPFLG),A
	JP	STR100
;
STRSYM:	LD	IX,SYMFCB
	SRV	MAKFCB
	IF	0
	LD	IY,SRCFCB
	CALL	COPYFN
	LD	(IX+9),"s"
	LD	(IX+10),"y"
	LD	(IX+11),"m"
	ENDIF
	LD	A,1
	LD	(SYMFLG),A
	JP	STR100
;
STRCRF:	LD	IX,CRFFCB
	SRV	MAKFCB
	IF	0
	LD	IY,SRCFCB
	CALL	COPYFN
	LD	(IX+9),"c"
	LD	(IX+10),"r"
	LD	(IX+11),"f"
	ENDIF
	LD	A,1
	LD	(CRFFLG),A
	JP	STR100
;
STRLN:	LD	A,1
	LD	(LNFLG),A
	JP	STR100
;
COPYFN:	KEEP	AF,BC,IX,IY
	LD	A,(IX+0)	;COPY DEVICE
	OR	A
	JR	NZ,CPFN10
	LD	A,(IY+0)
	LD	(IX+0),A
CPFN10:	LD	A,(IX+1)	;COPY F.N.
	CP	" "
	JR	NZ,CPFN30
	KEEP	IX,IY
	LD	B,8
CPFN20:	LD	A,(IY+1)
	LD	(IX+1),A
	INC	IY
	INC	IX
	DJNZ	CPFN20
	KEEPOUT
CPFN30:	KEEPOUT
	RET
;
	ENDIF
;
STR300:
;
	IF	DEBUG
	LD	IX,OBJFCB
	LD	(IX+0),$F0
	ENDIF
;
	CALL	CLEAR
	LD	HL,0
	LD	(REFER),HL
	LD	(OBJREF),HL
	IF	FILEVER
	LD	(ERRCNT),HL
	ENDIF
	XOR	A
	LD	(EQUFLG),A
;
	LD	A,1
	LD	(PASS),A
	CALL	APASS
;
;    SET UP FOR PASS-2(OPEN OUTPUT FILES!)
	LD	A,(PC88FLG)
	OR	A
	CALL	NZ,RESTORE_GRM
;
	LD	IX,OBJFCB
	LD	IY,SRCFCB
	CALL	COPYFN
	LD	A,(IX+9)	;1ST OF TYPE
	CP	" "
	JR	NZ,STR405
	LD	(IX+9),"i"
	LD	(IX+10),"m"
	LD	(IX+11),"g"
STR405:	
	LD	A,(OBJFLG)	;CHECK TO MAKE?
	OR	A
	JR	Z,STR410
	LD	A,(NOBUF)
	OR	A
	JR	Z,STR410
;
	LD	A,1
	LD	B,8
	LD	HL,FOBJBUF	;FOBJBUF(2048)
	LD	(IX+12),1	;MAKES IMG FILE
	SRV	OPEN
	LD	HL,(STARTADR)
	LD	A,L
	SRV	PUTCHR
	LD	A,H
	SRV	PUTCHR
	PUSH	HL
	LD	HL,(BUFREF)
	LD	DE,ZZZZZZ
	OR	A
	SBC	HL,DE
	POP	DE
	ADD	HL,DE
	LD	A,L
	SRV	PUTCHR
	LD	A,H
	SRV	PUTCHR
STR410:
;
        LD      A,(CRFFLG)
	OR	A
	JR	Z,STR390
	LD	IX,CRFFCB
	LD	IY,OBJFCB
	CALL	COPYFN
	LD	(IX+9),"c"
	LD	(IX+10),"r"
	LD	(IX+11),"f"
        LD      A,1
	LD	B,1
	LD	HL,FCRFBUF
	SRV	OPEN
	LD	A,(PAGES)
	SRV	PUTCHR
STR390:	LD	A,(LSTFLG)
	LD	B,A
	LD	A,(LBLFLG)
	OR	B
	JR	Z,STR400
	LD	IX,LSTFCB
	LD	A,1
	LD	B,1
	LD	HL,FLSTBUF
	SRV	OPEN
	XOR	A
	LD	(FIRSTFF),A
STR400:	
;
	LD	A,2
	LD	(PASS),A
	CALL	APASS
;
	LD	A,(OBJFLG)
	OR	A
	JR	Z,STR420
	LD	A,(NOBUF)
	OR	A
	JR	Z,STR420
	LD	IX,OBJFCB	;ONLY FILE MODE
	SRV	CLOSE
STR420:	LD	HL,ENDMES
	SRV	CPRSTR
	LD	A,(SYMFLG)
	LD	B,A
	LD	A,(LBLFLG)
	OR	B
	CALL	NZ,LABEL_SORT
	LD	A,(SYMFLG)
	OR	A
	CALL	NZ,PUTSYM
	LD	A,(LBLFLG)
	OR	A
	CALL	NZ,PUTLBL
;
	IF	FILEVER
	LD	IX,LSTFCB
	LD	HL,(ERRCNT)
	LD	A,L
	OR	H
	JR	Z,STRNOERR
;
	SRV	PUTNXT
	DEFB	"Total ERRORs: ",0
	SRV	PUTDEC
	SRV	PUTNXT
	DEFB	" (contains PASS 1 and 2)",$D,$A,0
	JR	STREEE
STRNOERR:	SRV	PUTNXT
	DEFB	"No ERRORs",$D,$A,0
;
STREEE:
	LD	A,(CRFFLG)
	OR	A
	JR	Z,STR990
	LD	IX,CRFFCB
	LD	A,$1A
	SRV	PUTCHR
	SRV	CLOSE
STR990:	
        ENDIF
	LD	A,(LBLFLG)
	LD	B,A
	LD	A,(LSTFLG)
	OR	B
	CALL	NZ,LSTFEED
	SRV	WARM
;
ENDMES:	DEFB	$D,$A,"Assemble Completed.",$D,$A,0
;
; LAST LPT FEED
LSTFEED:
	LD	IX,LSTFCB
	LD	A,(IX+0)
	CP	$F0
	JR	Z,LSTFE
	LD	A,12
	SRV	PUTCHR
LSTFE:	SRV	CLOSE
	RET
;
;
;
OPENSRCSUB:	
	LD	IX,(FCBA)
	LD	(IX+42),0	;NOT OPEN YET
	CALL	SETSRCPAR
	LD	HL,0-1
	LD	(IX+40),L
	LD	(IX+41),H
	CALL	GETRECSUB
	LD	A,(IX+38)
	OR	(IX+39)
	JP	Z,NOEND
	RET
;
OPENSRC_DO:	
	KEEP	IY
	LD	IX,(FCBA)
	LD	(IX+42),1
	LD	IY,DMYFCB
	SRV	SCHFST
	JP	C,OPENERR
	LD	A,2
	LD	B,8
	LD	HL,(BUFA)
	SRV	OPEN
	KEEPOUT
        RET
;
;
GETRECSUB:	
	LD	(IX+38),0
	LD	(IX+39),0
        PUSH    BC
	PUSH	HL
	LD	HL,(BUFA)
        LOOP    B,BUFSIZE
	CALL	GETRECSUBSUB
	JR	C,GETRECSUBERR
	KEEP	DE
	LD	DE,8*256
	ADD	HL,DE
	KEEPOUT
	LEND
	POP	HL
	POP	BC
        OR      A
        RET
GETRECSUBERR:	
	POP	HL
        POP     BC
	RET
;
GETRECSUBSUB:	
	LD	A,(PC88FLG)
	OR	A
	JR	Z,GRSS10
; if not OUTREADFLG then call READGRM else ...
	LD	A,(OUTREADFLG)
	OR	A
	JR	NZ,GRSS10
	KEEP	HL,DE,BC
	LD	L,(IX+40)
	LD	H,(IX+41)
	INC	HL
	LD	(IX+40),L
	LD	(IX+41),H
	CALL	READGRM
	LD	A,(IX+39)
	ADD	A,8
	LD	(IX+39),A
	KEEPOUT
	OR	A
	RET
;
GRSS10:	KEEP	HL,DE,BC
	LD	A,(IX+42)
	OR	A
	CALL	Z,OPENSRC_DO
	LD	L,(IX+40)
	LD	H,(IX+41)
	INC	HL
	LD	(IX+40),L
	LD	(IX+41),H
	SRV	SETREC
	KEEPOUT
	RET	C
        KEEP    HL,DE,BC
	SRV	SETBUF
	SRV	GETREC
;
	LD	A,(PC88FLG)
	OR	A
	CALL	NZ,SETGRM
;
	LD	A,(IX+39)
	ADD	A,8
	LD	(IX+39),A
        KEEPOUT
	RET
;
SETSRCPAR:	
	KEEP	HL
	LD	HL,(BUFA)
	LD	(IX+36),L
	LD	(IX+37),H
;        LD      (IX+38),0
;        LD      (IX+39),BUFSIZE
	KEEPOUT
	RET
;
OPENS:	LD	HL,BUF
	LD	(BUFA),HL
	LD	IX,SRCFCB
	LD	(FCBA),IX
	CALL	OPENSRCSUB
	XOR	A
	LD	(EOFFLG),A
	LD	(INCFLG),A
	LD	L,A
	LD	H,A
	LD	(FILEP),HL
	RET
;
OPENERR:	
	SRV	CPRNXT
	DEFB	$D,$A,"File not Found",$D,$A,7,0
	SRV	RESET
	SRV	WARM
;
LINEIN:	LD	DE,LINBUF
        LD      IX,(FCBA)
	LD	C,(IX+38)
	LD	B,(IX+39)	;COUNTER
	EXX
	EX	AF,AF'
	LD	HL,(FILEP)
	LD	A,(INCFLG)
	OR	A
	EX	AF,AF'
	EXX
	LD	L,(IX+36)
	LD	H,(IX+37)	;POINTER
LIN10:	LD	A,B
	OR	C
	JR	NZ,LIN100
	CALL	GETRECSUB
	JP	C,LIN200	;MEANS NOEND(OUT OF RECORD)
	CALL	SETSRCPAR
	LD	C,(IX+38)
	LD	B,(IX+39)	;COUNTER
	LD	A,C
	OR	B
	JP	Z,LIN200	;CAN NOT GET ANY,END OF FILE
        LD      HL,(BUFA)       ;POINTER; SAME AS LD HL,(IX+36)
LIN100: DEC     BC
	LD	A,(HL+)		;GET ONE CHR
	CP	$0A
	JR	Z,LIN10
	EXX
	EX	AF,AF'
	JR	NZ,LIN110
	INC	HL
LIN110:	EX	AF,AF'
	EXX
	CP	$1A
	JP	Z,LIN200
	LD	(DE+),A		;SET TO LINBUF
	CP	$0D
	JR	NZ,LIN10	;NOT EOL THEN LOOP
	LD	(IX+38),C
	LD	(IX+39),B
	LD	(IX+36),L
	LD	(IX+37),H
	EXX
	LD	(FILEP),HL
	EXX
	LD	IY,LINBUF
	LD	HL,(LINENUM)
	INC	HL
	LD	(LINENUM),HL
	LD	A,(CRFFLG)
	OR	A
	RET	Z
	LD	A,(PASS)
	CP	1
	RET	Z
	KEEP	IX
	LD	IX,CRFFCB
	LD	A,$0D
	SRV	PUTCHR
	KEEPOUT
	RET
;
LIN200:	LD	A,(IX+42)
	OR	A
	JR	Z,LIN210
	SRV	CLOSE
LIN210:	LD	A,(INCFLG)
	OR	A
	JP	Z,NOEND
	DEC	A
	LD	(INCFLG),A
	CALL	STKPOP
	LD	A,L
	CALL	STKPOP
	EX	DE,HL
	CALL	STKPOP
	CP	17
	JP	NZ,STKMIS
	LD	(FCBA),HL
	LD	(BUFA),DE
	SRV	CPRNXT
	DEFB	" .",$D,$A,0
	LD	A,(TWNFLG)
	OR	A
	JP	Z,LINEIN
	LD	HL,INCFCB
	LD	DE,IMGFCB
	LD	BC,36
	LDIR
	LD	IX,IMGFCB
	LD	(IX+9),"i"
	LD	(IX+10),"m"
	LD	(IX+11),"g"
	CALL	MINCLIMG2
	JP	LINEIN
;
CLOSES:	LD	IX,(FCBA)
	LD	A,(IX+42)
	OR	A
	RET	Z
	SRV	CLOSE
	RET
;
APASS:	SRV	CPRNXT
	DEFB	$D,$A,"Pass: ",0
	LD	A,(PASS)
	ADD	A,"0"
	SRV	CPRCHR
	SRV	CPRCRLF
	CALL	STKINI
	LD	HL,0
	LD	(REFER),HL
	LD	(OBJREF),HL
	LD	(LINENUM),HL
	IF	FILEVER
	LD	HL,ZZZZZZ
	ENDIF
	LD	(BUFREF),HL
	IF	FILEVER
	LD	HL,0-ZZZZZZ
	ENDIF
	LD	(OBJOFF),HL
	XOR	A
	LD	(XLSTFLG),A
	LD	(XCRFFLG),A
	LD	(ORGFLG),A
	IF	FILEVER
	LD	(SUBTTLBUF),A
	LD	HL,0
	LD	(PGNUM),HL
	DEC	A
	LD	(PGCOUNT),A
	LD	A,60
	LD	(PAGES),A
	ENDIF
	CALL	OPENS
APS100:	CALL	LINEIN
	IF	0
	PUSH	IY
	POP	HL
	SRV	CPR4HEX
	SRV	CPRSPC
	REPEAT
	LD	A,(HL+)
	SRV	CPRCHR
	CP	13
	UNTIL	Z
	SRV	CPRCRLF
	ENDIF
        CALL    ALINE
        LD      A,(EOFFLG)
	OR	A
	JR	Z,APS100
	CALL	CLOSES
	RET
;
;
NEXTLINE:
	LD	SP,(SPKEEP)
	PUSH	IY
	POP	HL
	LD	A,13
	LD	BC,0
	CPIR
	PUSH	HL
	POP	IY
	RET
;
ALINE_INI:	
	LD	HL,WORKS_S
	LD	DE,WORKS_S+1
	LD	BC,WORKS_I-WORKS_S-1
	LD	(HL),0
	LDIR
	RET
;
ALINE:	LD	(SPKEEP),SP
	SRV	STPESC
	LD	(LINTOP),IY
	LD	HL,WORKS_S
	LD	DE,WORKS_S+1
	LD	BC,WORKS_E-WORKS_S-1
	LD	(HL),0
	LDIR
;
	LD	A,(IY+0)
	CP	9		;TAB CODE
	JP	Z,ALN170
        CP      "."
	JP	Z,ALN170
	CALL	WSR
	JR	C,ALN100
	LD	A,C
	CP	" "
	JR	Z,ALN170
	CP	9		;TAB
	JR	Z,ALN170
	DEC	IY
	CP	";"
	JP	Z,ALNEEN
	CP	"*"		;SPECIAL COMMENT CHARACTER
	JP	Z,ALNEEN
	CP	13		;EOL
	JP	Z,ALNEEN
	JP	ILLNAM
;
ALN100:	LD	A,C
	CP	$F0
	JP	C,ALN180
	LD	A,(PASS)
	CP	2
	JR	Z,ALN150
	LD	A,B
	AND	C
	INC	A
	JP	NZ,DBLNAM
ALN150:	LD	A,(IY+0)
	INC	IY
	CP	":"
	JP	NZ,EQUONL
	LD	A,1
	LD	(COLLES),A
	LD	HL,(IDPOS)
	LD	(LSTPOS),HL
	LD	A,(CRFFLG)
	OR	A
	CALL	NZ,PUTDNAM
	LD	A,(PASS)
	CP	2
	JR	Z,ALN170
	LD	BC,(REFER)
	CALL	SETLBL
	CALL	FREEZE
ALN170:
;
; MNEMONIC ANALIZE
; ””””””””””””””””
	CALL	SSKIP
	CALL	TSTEOL
	JP	Z,ALNEEE
	LD	A,1
	LD	(EXIMNM),A
;
	IF	HIGHSPEED
;PACHED FOR VER 3.0
	LD	(HMODE),A	;A=1
	LD	(HSPKEEP),SP
	LD	(HPTRKEEP),IY
;HSEARCH:
	LD	A,(IY+0)
	INC	IY
        CP      "Z"+1
	JP	NC,INSERR
        SUB     "A"
	JP	C,INSERR
	LD	(HSEARPTR),IY
	ADD	A,A
        LD      L,A
	LD	H,0
	LD	DE,HINDEX
	ADD	HL,DE
	LD	E,(HL+)
	LD	D,(HL)
	LD	(HSEARVECT),DE
HSEARPTR	EQU	$+1
	LD	HL,0
HSEARVECT	EQU	$+1
	JP	0
;
INSOK:	PUSH	HL
	POP	IY
	LD	(PREBYT),BC
	IF	INDICATE
	SRV	CPRNXT
	DEFB	"!",0
	ENDIF
	JP	ALNEEEE
;
INSERR:	
	LD	SP,(HSPKEEP)
	LD	IY,(HPTRKEEP)
	CALL	ALINE_INI
	XOR	A
	LD	(HMODE),A
	IF	INDICATE
	LD	A,"%"
	SRV	CPRCHR
	ENDIF
	ENDIF
;
;
	CALL	WSR
	JP	NC,ILLMNM
ALN180:	
;        LD      A,B
;        DEC     A
;        JP      NZ,ILLMNM       ;CP 1
	LD	A,B		;IS(B 1)
	LD	(EXIMNM),A
	LD	A,C
	CP	$F0		;MAXMNM+1
	JP	NC,ILLMNM
	LD	HL,(VALUE)
	LD	A,(HL)
	CP	1
	JP	NZ,EXTMNM
	INC	HL
	LD	(WKEEP),HL
	INC	HL
	INC	HL
	LD	A,(HL)
	CP	4
	JR	NC,ALNGIJ
	CALL	SSKIP
	CALL	TSTEOL
	LD	A,1
	JR	Z,ALN2SH
	CALL	ANAOP
	LD	(NUM1),DE
	LD	(TBL1),HL
	LD	A,(IY+0)
	CP	","
	LD	A,2
	JR	NZ,ALN2SH
ALNLLL:	INC	IY
	CALL	RSVPP
	CALL	ANAOP
	LD	(NUM2),DE
	LD	(TBL2),HL
	LD	A,3
ALN2SH:	LD	(TKEEP),A
	LD	HL,(WKEEP)
	JR	ALN2EE
;
ALN2EN:	LD	HL,(NXTWK)
ALN2EE:	LD	E,(HL)
	INC	HL
	LD	D,(HL)
	INC	HL
	LD	(NXTWK),DE
ALN2NN:	LD	A,(HL)
	OR	A
	JP	Z,ILLFRM
	LD	A,(TKEEP)
	CP	(HL)
	JR	NZ,ALN2EN
	INC	HL
	DEC	A
	JR	Z,NOOP
	DEC	A
	JR	Z,ONEOP
	DEC	A
	JP	Z,TWOOP
	JP	INTERR
;
ALNGIJ:	CP	27+1
	JP	NC,INTERR
	PUSH	AF
	SUB	4
	LD	L,A
	LD	H,0
	POP	AF
	ADD	HL,HL
	LD	DE,GIJTBL
	ADD	HL,DE
	LD	E,(HL)
	INC	HL
	LD	D,(HL)
	EX	DE,HL
	JP	(HL)
;
GIJTBL:	DEFW	MORG
	DEFW	MEQU
	DEFW	MDEFB
	DEFW	MDEFW
	DEFW	MDEFS
	DEFW	INTERR
	DEFW	MEND
	DEFW	MZ80
	DEFW	MPHASE
	DEFW	MDEPHASE
	DEFW	MIF
	DEFW	MENDIF
	DEFW	MELSE
	DEFW	MINCLUDE
	DEFW	MTITLE
	DEFW	MSUBTTL
	DEFW	MLIST
	DEFW	MXLIST
	DEFW	MPAGE
	DEFW	INTERR
	DEFW	MINCLIMG
	DEFW	MINCLTWN
	DEFW	MCRF
	DEFW	MXCRF
;
NOOP:	CALL	SETOPC
	JP	ALNEEE
;
SETOPC:	LD	A,(HL)
	OR	A
	JR	Z,SOP10
	LD	(PREBYT),A
SOP10:	INC	HL
	LD	A,(HL)
	LD	(OPEBYT),A
	RET
;
ONEOP:	EX	DE,HL
	INC	DE
	INC	DE
	LD	A,(DE)
	LD	B,A
	LD	HL,(TBL1)
OOP100:	LD	A,(HL)
	INC	HL
	CP	255
	JP	Z,ALN2EN
	CP	B
	JR	Z,OOP200
	INC	HL
	JR	OOP100
OOP200:	LD	A,(HL)
	LD	(ORP1),A
	LD	A,B
	LD	(OP1),A
	DEC	DE
	DEC	DE
	EX	DE,HL
	CALL	SETOPC
	LD	A,(ORP1)
	LD	B,A
	LD	A,(OP1)
	LD	HL,(NUM1)
	CALL	SETOPR
	JP	ALNEEE
;
TWOOP:	EX	DE,HL
	INC	DE
	INC	DE
	LD	A,(DE)
	LD	B,A
	INC	DE
	LD	A,(DE)
	LD	C,A
TOP80:	LD	HL,(TBL1)
TOP100:	LD	A,(HL)
	INC	HL
	CP	255
	JP	Z,ALN2EN
	CP	B
	JR	Z,TOP200
TOP190:	INC	HL
	JR	TOP100
;
TOP200:	LD	IX,(TBL2)
TOP300:	LD	A,(IX+0)
	INC	IX
	CP	255
	JR	Z,TOP190
	CP	C
	JR	Z,TOP400
	INC	IX
	JR	TOP300
;
TOP400:	PUSH	IX
	LD	A,(HL)
	LD	(ORP1),A
	LD	A,B
	LD	(OP1),A
	POP	HL
	LD	A,(HL)
	LD	(ORP2),A
	LD	A,C
	LD	(OP2),A
	DEC	DE
	DEC	DE
	DEC	DE
	EX	DE,HL
	CALL	SETOPC
;
	LD	A,(ORP1)
	LD	B,A
	LD	A,(OP1)
	LD	HL,(NUM1)
	CALL	SETOPR
	LD	A,(ORP2)
	LD	B,A
	LD	A,(OP2)
	LD	HL,(NUM2)
	CALL	SETOPR
	LD	A,(PREBYT)
	OR	A
	JP	NZ,ALNEEE
	LD	A,(OPEBYT)
	CP	$76
	JP	NZ,ALNEEE
	JP	ILLOPR
;
MORG:	LD	A,1
	LD	(EQUFLG),A
	LD	(GIJFLG),A
	CALL	EXPRES
	XOR	A
	LD	(EQUFLG),A
	LD	(REFER),HL
	LD	(OBJREF),HL
	IF	FILEVER
	LD	A,(ORGFLG)
	OR	A
	JR	Z,MORG10
	LD	DE,(OBJOFF)
	OR	A
	SBC	HL,DE
	LD	(BUFREF),HL
	JP	ALNEEE
;
MORG10:	PUSH	HL
	LD	HL,(REFER)
	LD	(STARTADR),HL
	POP	HL
	LD	DE,ZZZZZZ
	LD	(BUFREF),DE
	OR	A
	SBC	HL,DE
	LD	(OBJOFF),HL
	LD	A,1
	LD	(ORGFLG),A
	ELSE
	LD	(BUFREF),HL
	ENDIF
	JP	ALNEEE
;
MDW:	
MDEFW:	CALL	SSKIP
	LD	DE,OBJLINBUF
	LD	C,0
MDW50:	PUSH	BC
	PUSH	DE
	CALL	EXPRES
	POP	DE
	POP	BC
	LD	A,L
	PUSH	HL
	CALL	MDPUT
	POP	HL
	LD	A,H
	CALL	MDPUT
	CALL	SSKIP
	CALL	TSTEOL
	JP	Z,MDB300
	LD	A,(IY+0)
	INC	IY
	CP	","
	JR	Z,MDW50
	JP	ILLFRM
;
MDB:	
MDEFB:	CALL	SSKIP
	LD	DE,OBJLINBUF
	LD	C,0
MDB50:	LD	A,(IY+0)
	CP	"'"
	JR	Z,MDB500
	CP	34
	JR	Z,MDB500
MDB100:	PUSH	BC
	PUSH	DE
	CALL	EXPRES
	POP	DE
	POP	BC
	LD	A,L
	CALL	MDPUT
MDB200:	CALL	SSKIP
	INC	IY
	CALL	TSTEOL
	JR	Z,MDB300
	CP	","
	JR	Z,MDB50
	JP	ILLFRM
;
MDB500:	CP	(IY+2)
	JR	Z,MDB100
	INC	IY
	LD	B,A
MDB600:	LD	A,(IY+0)
	INC	IY
	CP	B
	JR	Z,MDB200
	PUSH	BC
	CALL	MDPUT
	LD	A,C
	POP	BC
	LD	C,A
	JR	MDB600
;
MDPUTCLOSE:
MDB300:	
	LD	A,C
	OR	A
	RET	Z
	CALL	MDPUT2
	LD	A,1
	LD	(HIDELST),A
	RET
;
MDPUT:	PUSH	AF
	LD	(DE+),A
	INC	C
	LD	A,C
	CP	4
	JR	NZ,MDPUTE
	POP	AF
MDPUT2:	PUSH	AF
	LD	A,C
	LD	(OBJCNT),A
	KEEP	BC,HL,IX
	CALL	PUTFUN
	KEEPOUT
	LD	A,1		;"erase the line
	LD	(HIDELST),A
	LD	DE,OBJLINBUF
	LD	C,0
MDPUTE:	POP	AF
	RET
;
PUT1IMG:	
	PUSH	AF
	LD	(DE+),A
	INC	C
	LD	A,C
	CP	16
	JR	NZ,P1IMGE
P1IMGC:	LD	(OBJCNT),A
	KEEP	BC,HL,IX
	CALL	PUTFUN
	KEEPOUT
	LD	DE,OBJLINBUF
	LD	C,0
P1IMGE:	POP	AF
	RET
;
PUTCLOSEIMG:	
	PUSH	AF
	LD	A,C
	OR	A
	JR	NZ,P1IMGC
	POP	AF
	RET
;
;
MDS:	
MDEFS:	LD	A,1
	LD	(EQUFLG),A
	LD	(GIJFLG),A
	CALL	EXPRES
	XOR	A
	LD	(OBJCNT),A
	LD	(OBJSKP),HL
	LD	(EQUFLG),A
	JP	ALNEEE
;
MEQU:	LD	A,(COLLES)
	OR	A
	JP	Z,ILLFRM
	LD	A,1
	LD	(EQUFLG),A
	CALL	EXPRES
	LD	(NUM1),HL
	XOR	A
	LD	(EQUFLG),A
	LD	A,(PASS)
	CP	2
	JP	Z,EQUCOM
	LD	C,L
	LD	B,H
	LD	DE,(LSTPOS)
	LD	(IDPOS),DE
	CALL	SETLBL
	JP	EQUCOM
;
M.PHASE:	
MPHASE:	
	LD	HL,(REFER)
	LD	DE,(OBJREF)
	OR	A
	SBC	HL,DE
	CALL	STKPUSH
	LD	HL,12
	CALL	STKPUSH
	LD	A,1
	LD	(EQUFLG),A
	CALL	EXPRES
	XOR	A
	LD	(OBJCNT),A
	LD	(EQUFLG),A
	LD	(REFER),HL
	JP	ALNEEN
;
M.DEPHASE:	
MDEPHASE:	
	CALL	STKPOP
	LD	A,L
	CALL	STKPOP
	CP	12
	JP	NZ,STKMIS
	LD	DE,(OBJREF)
	ADD	HL,DE
	LD	(REFER),HL
	JP	ALNEEN
;
MINCLIMG:	
	LD	A,(INCFLG)
	OR	A
	JP	NZ,INCERR
	CALL	SSKIP
	LD	IX,IMGFCB
	PUSH	IY
	POP	HL
	SRV	MAKFCB
	PUSH	HL
	POP	IY
	CALL	SETIMG
	CALL	SSKIP
	CALL	TSTEOL
	JP	NZ,AFTWRT
	LD	A,1
	LD	(GIJFLG),A
	KEEP	IX
	CALL	PUTFUN2
	KEEPOUT
MINCLIMG2:	
	SRV	CPRNXT
	DEFB	"incl.img ",0
	CALL	PRFN
	SRV	CPRCRLF
	LD	A,1
	LD	(IMGMODE),A
	LD	A,0
	LD	B,8
	LD	HL,BUF2
	SRV	OPEN
	JP	C,OPENERR
	LD	A,(IX+12)
	AND	1
	JR	Z,MIMG_BLK
	SRV	GETCHR
	LD	E,A
	SRV	GETCHR
	LD	D,A
	SRV	GETCHR
	LD	L,A
	SRV	GETCHR
	LD	H,A
	OR	A
	SBC	HL,DE
	LD	C,0
	LD	DE,OBJLINBUF
	LOOP	HL
	SRV	GETCHR
	CALL	PUT1IMG
	LEND	A
	JP	MIMGEEE
;
MIMG_BLK:	
	LD	C,0
	LD	DE,OBJLINBUF
MIMGB10:	
	SRV	GETCHR
	JR	C,MIMGEEE
	CALL	PUT1IMG
	JR	MIMGB10
;
MIMGEEE:	
	CALL	PUTCLOSEIMG
	SRV	CLOSE
	XOR	A
	LD	(IMGMODE),A
	LD	(TWNFLG),A
	SRV	CPRNXT
	DEFB	" .",$D,$A,0
	RET
;
MINCLTWN:	
	LD	A,1
	LD	(TWNFLG),A
;
MINCLUDE:
	LD	A,(INCFLG)
	OR	A
	JP	NZ,INCERR
	CALL	SSKIP
	CP	34
	JR	NZ,MINC1
	INC	IY
MINC1:	LD	IX,INCFCB
	PUSH	IY
	POP	HL
        SRV     MAKFCB
	PUSH	HL
	POP	IY
        CALL    SETAZA
	SRV	CPRNXT
	DEFB	"include  ",0
	CALL	PRFN
	SRV	CPRCRLF
	CALL	SSKIP
	CALL	TSTEOL
	JP	NZ,AFTWRT
	LD	HL,(FCBA)
	CALL	STKPUSH
	LD	HL,(BUFA)
	CALL	STKPUSH
	LD	HL,17
	CALL	STKPUSH
	LD	A,(INCFLG)
	INC	A
	LD	(INCFLG),A
	LD	(FCBA),IX
	LD	HL,BUF2
	LD	(BUFA),HL
	CALL	OPENSRCSUB
	JP	ALNEEN
;
MIF:	LD	A,1
	LD	(EQUFLG),A	;TO CALCULATE in PASS 1
	LD	A,14
	LD	(GIJFLG),A	;NOT NORMALY INSTRUCTION
	CALL	EXPRES		;CALC LOGICAL EXPR.
	XOR	A
	LD	(IFNEST),A	;NESTING CLEAR
	LD	(EQUFLG),A	;CALC in PASS 1 TO CLEAR
	LD	A,L
	OR	H
	JR	Z,MIF10		;IF VALUE=0 ?
	LD	HL,$FFFF	;ELSE VALUE=$FFFF
MIF10:	LD	(CONDIT),HL	;ANSWER OF CONDITION
	PUSH	HL
	CALL	PUTFUN2		;PUT THIS LINE ( IF exp)
	LD	HL,14
	CALL	STKPUSH		;PUSH STACK I.D. CODE
	POP	AF		;RESTORE CONDITION
	OR	A		;TRUE or FALSE ?
	JR	Z,MIFALSE	;IF FALSE then GO TO SKIP
	RET			;NORMALY END
MIFALSE:
	LD	A,16
	LD	(GIJFLG),A
	SRV	STPESC
	CALL	LINEIN
	LD	(LINTOP),IY
	XOR	A
	LD	(HIDELST),A
	LD	A,(IY+0)
	CP	32
	JR	Z,MIF100
	CP	9
	JR	Z,MIF100
	CALL	WSRXX
	CALL	TSTEOL
	JR	Z,MIFEEE
	CALL	WSRXX
	CALL	TSTEOL
	JR	Z,MIFEEE
MIF100:	CALL	SSKIP_WSR
	DEC	B
	LD	A,C
	JR	NZ,MIFEEE
	CP	77		;IF
	JR	Z,MIFINC
	CP	78		;ENDIF
	JR	Z,MIFEND
	CP	79		;ELSE
	JR	Z,MIFELS
MIFEEE:	LD	A,(NIFFLG)
	OR	A
	CALL	Z,PUTFUN2
	JR	MIFALSE
MIFINC:	LD	A,(IFNEST)
	INC	A
MIFI10:	LD	(IFNEST),A
	JR	MIFEEE
MIFEND:	LD	A,(IFNEST)
	OR	A
	JR	Z,MIFEXIT
	DEC	A
	JR	MIFI10
MIFEXIT:
	CALL	STKPOP
MIFELS:	LD	A,(IFNEST)
	OR	A
	JR	NZ,MIFEEE
	LD	A,1
	LD	(GIJFLG),A
	CALL	PUTFUN2
	RET
;
MENDIF:	CALL	STKPOP
	LD	DE,14
	OR	A
	SBC	HL,DE
	JP	NZ,STKMIS
	LD	A,1
	LD	(GIJFLG),A
	JP	ALNEEE
;
MELSE:	CALL	STKLOOK
	LD	DE,14
	OR	A
	SBC	HL,DE
	JP	NZ,STKMIS
	LD	A,1
	LD	(GIJFLG),A
	XOR	A
	LD	(IFNEST),A
	CALL	PUTFUN2
	JP	MIFALSE
;
MXCRF:	DEFB	$3E
MCRF:	XOR	A
	LD	(XCRFFLG),A
	JP	ALNEEN
;
M.XLIST:
MXLIST:	DEFB	$3E
M.LIST:	
MLIST:	XOR	A
	LD	(XLSTFLG),A
	LD	A,1
	LD	(ONCEXLST),A
	JP	ALNEEN
;
	IF	FILEVER
MTITLE:
	CALL	SSKIP
	LD	A,(IY+0)
	CP	34
	JR	NZ,MTIT05
	INC	IY
MTIT05:	LD	IX,DMYFCB
	LD	A,(OBJFCB+1)
	CP	" "
	JR	NZ,MTIT10
	LD	A,(PASS)
	DEC	A
	JR	NZ,MTIT10
	LD	IX,OBJFCB
MTIT10:	
	PUSH	IY
	POP	HL
	SRV	MAKFCB
	PUSH	HL
	POP	IY
	CALL	SETIMG
	CALL	SSKIP
	CALL	TSTEOL
	JP	Z,ALNEEN
	JP	AFTWRT
;
MSUBTTL:	
	CALL	SSKIP
	LD	DE,SUBTTLBUF
	LD	B,60
MST20:	LD	A,(IY+0)
	INC	IY
	CALL	TSTEOL
	JR	Z,MSTEEE
	LD	(DE+),A
	DJNZ	MST20
MST30:	LD	A,(IY+0)
	INC	IY
	CALL	TSTEOL
	JR	NZ,MST30
MSTEEE:	DEC	IY
	XOR	A
	LD	(DE),A
	JP	ALNEEN
;
MPAGE:	CALL	SSKIP
	CALL	TSTEOL
	JR	Z,MPAGE1
	CALL	EXPRES
	LD	H,0
	LD	(NUM1),HL
	LD	A,L
	PUSH	AF
	LD	A,4
	LD	(GIJFLG),A
	CALL	ALNEEE
	POP	AF
	LD	(PAGES),A
MPAGECOM:	
	LD	A,255
	LD	(PGCOUNT),A
	RET
MPAGE1:	CALL	ALNEEN
	JR	MPAGECOM
;
;
NEWPAGE:	
	LD	IX,LSTFCB
	LD	A,(FIRSTFF)
	OR	A
	JR	Z,NPG00
        LD      A,(IX+0)
	CP	$F0
	LD	A,12
	JR	Z,NPG00
	SRV	PUTCHR
NPG00:	LD	A,1
	LD	(FIRSTFF),A
	LD	HL,OBJFCB+1
	LD	B,8
	CALL	NPGSUB
	LD	A,"."
	SRV	PUTCHR
	LD	B,3
	CALL	NPGSUB
	LD	B,8
	CALL	NPGSPC
	CALL	PUTDATE
	LD	B,12
	CALL	NPGSPC
	SRV	PUTNXT
	DEFB	"AZA assembler",0
	LD	B,10
	CALL	NPGSPC
	SRV	PUTNXT
	DEFB	"PAGE ",0
	LD	HL,(PGNUM)
	INC	HL
	LD	(PGNUM),HL
	SRV	PUTDEC
	SRV	PUTCRLF
	LD	HL,SUBTTLBUF
	SRV	PUTSTR
	SRV	PUTCRLF
	SRV	PUTCRLF
	RET
;
PUTDATE:
	LD	HL,DATEBUF
	XOR	A
	SRV	9
	LD	A,(DATEBUF+5)
	SRV	PUT2HEX
	LD	A,"/"
	SRV	PUTCHR
	LD	A,(DATEBUF+4)
	SRV	PUT2HEX
	LD	A,"/"
	SRV	PUTCHR
	LD	A,(DATEBUF+3)
	SRV	PUT2HEX
	RET
;
NPGSUB:	LD	A,(HL+)
	SRV	PUTCHR
	DJNZ	NPGSUB
	RET
;
NPGSPC:	SRV	PUTSPC
	DJNZ	NPGSPC
	RET
;
	ELSE
;"no support if EDT/ASM
MTITLE:
MSUBTTL:
MPAGE:	PUSH	IY
	POP	HL
	LD	BC,0
	LD	A,13
	CPIR
	DEC	HL
	PUSH	HL
	POP	IY
	JP	ALNEEN
;
	ENDIF
;
MEND:	CALL	STKCLOS
	CALL	SSKIP
	CALL	TSTEOL
	JR	Z,MEND1
;"set exec adr
	CALL	EXPRES
	KEEP	IY
	SRV	ALOMAP
	LD	(IY+6),L
	LD	(IY+7),H
	KEEPOUT
MEND1:	LD	A,1
	LD	(EOFFLG),A
MZ80:
ALNEEN:	LD	A,1
	LD	(GIJFLG),A
ALNEEE:	CALL	SSKIP
	CALL	TSTEOL
	JP	NZ,AFTWRT
ALNEEEE:	
;
PUTFUN2:	
	LD	A,1
	LD	(FRMFLG),A
PUTFUN:	LD	A,(PPFLG)
	OR	A
	JR	NZ,PUTFUN10
	LD	A,(PP_EXIST)
	OR	A
	JR	Z,PUTFUN10
        LD      IX,PRECODE
	CALL	PUTPP
PUTFUN10:
	LD	A,(PPFLG)
	OR	A
	JR	NZ,PUTFUN20
	LD	A,(FRMFLG)
	OR	A
	CALL	NZ,FRMOBJ
PUTFUN20:	
	LD	A,(PASS)	;TO LIST OUT or NOT
	DEC	A
	JR	Z,PFN100	;IF PASS 1 THEN NEVER PUT ANY
	LD	A,(XLSTFLG)
	OR	A
	JR	NZ,PFN90
	LD	A,(ONCEXLST)
	OR	A
	JR	NZ,PFN90
	LD	A,(LSTFLG)
	OR	A
	CALL	NZ,PUTLST
	LD	A,1
	LD	(HIDELST),A
PFN90:	LD	A,(OBJFLG)	;TO OBJECT PUT OUT or NOT
	OR	A
	CALL	NZ,PUTOBJ
PFN100:	LD	A,(OBJCNT)
	LD	HL,(OBJSKP)
	LD	E,A
	LD	D,0
	ADD	HL,DE
	EX	DE,HL
	LD	HL,(REFER)
	ADD	HL,DE
	LD	(REFER),HL
	LD	HL,(OBJREF)
	ADD	HL,DE
	LD	(OBJREF),HL
	LD	HL,(BUFREF)
	ADD	HL,DE
	LD	(BUFREF),HL
;
	LD	A,(PPFLG)
	OR	A
	RET	NZ
;
	LD	A,(PP_EXIST)
	OR	A
	RET	Z
;
	LD	IX,POSTCODE	;POST INC/DEC
	CALL	PUTPP
	CALL	CLRPP
	RET
;
;
PUTPP:	LD	A,1
	LD	(PPFLG),A
	LD	DE,OBJLINBUF
	LD	C,0
	LD	L,4
PUTPP10:	
	LD	A,(IX+0)	;COUNT OF PUT OUT
	OR	A
	JR	Z,PUTPP30
	LD	B,A
	LOOP	B
	LD	A,(IX+1)	;PUT PRE-CODE
	OR	A
	CALL	NZ,MDPUT
	LD	A,(IX+2)	;PUT MAIN CODE
	OR	A
	JP	Z,ILLOPR
	CALL	MDPUT
	LEND
PUTPP30:	
	INC	IX
	INC	IX
	INC	IX
	DEC	L
	JR	NZ,PUTPP10
	CALL	MDB300
	XOR	A
	LD	(PPFLG),A
	RET
;
PUTOBJ:	CALL	PUTOBJ0
	LD	DE,(OBJSKP)
	LD	A,E
	OR	D
	RET	Z
	LD	A,(NOBUF)
	OR	A
	JR	Z,POB00
	KEEP	IX
	LD	IX,OBJFCB
	LOOP	DE
	XOR	A
	SRV	PUTCHR
	LEND	A
	KEEPOUT
	RET
POB00:	LOOP	DE
	LD	(HL+),0
	LEND	A
	RET
;
PUTOBJ0:
	LD	HL,(BUFREF)
	LD	DE,OBJLINBUF
	LD	A,(OBJCNT)
	OR	A
	RET	Z
	LD	B,A
	LD	A,(NOBUF)
	OR	A
	JR	NZ,POB200
POB100:	LD	A,(DE+)		;ON BUFFER MODE
	LD	(HL+),A
	DJNZ	POB100
	RET
;
POB200:	KEEP	IX
	LD	IX,OBJFCB
	LOOP	B
	LD	A,(DE+)
	SRV	PUTCHR
	LEND
	KEEPOUT
	RET
;
PUTLST:	IF	FILEVER
	LD	A,(PGCOUNT)
	LD	B,A
	LD	A,(PAGES)
	SUB	3+1
	CP	B
	JR	NC,PTL10
	XOR	A
	LD	(PGCOUNT),A
	CALL	NEWPAGE
PTL10:	LD	A,(PGCOUNT)
	INC	A
	LD	(PGCOUNT),A
	ENDIF
	LD	HL,LSTBUF
	PUSH	HL
	LD	B,24
	LD	A,(IMGMODE)
	OR	A
	JR	Z,PTL20
	LD	B,79
PTL20:	LD	A," "
PTL50:	LD	(HL),A
	INC	HL
	DJNZ	PTL50
	LD	(HL),0
	POP	HL
	IF	FILEVER
	LD	A,(ERRORED)
	OR	A
	JP	NZ,PTLERR
	ENDIF
	LD	A,(GIJFLG)
	CP	2
	JR	Z,PTL150
	CP	4
	JR	Z,PTL500
	CP	14
	JP	Z,PTL600
	CP	16
	JP	Z,PTL700
	INC	HL
	LD	DE,(REFER)
	CALL	SET4H
	LD	DE,OBJLINBUF
	LD	A,(OBJCNT)
	OR	A
	JR	Z,PTL150
	LD	B,A
	KEEP	BC,DE
PTL100:	INC	HL
	LD	A,(DE)
	CALL	SET2H
	INC	DE
	DJNZ	PTL100
	KEEPOUT
	LD	A,(IMGMODE)
	OR	A
	JR	Z,PTL150
	LD	HL,LSTBUF+5+48+5
PTL110:	LD	A,(DE+)
	CP	32
	JR	NC,PTL120
	LD	A,"."
PTL120:	LD	(HL+),A
	DJNZ	PTL110
	LD	(HL),0
;
PTL150:	
	LD	IX,LSTFCB
	LD	HL,(LINENUM)
        LD      A,(LNFLG)
	OR	A
	CALL	NZ,PUTDECSUB
        LD      HL,LSTBUF
        SRV     PUTSTR
	LD	A,(HIDELST)	;IF ALREADY LISTED THEN HIDE TO LIST
	OR	A
	JR	NZ,PTL400
	LD	A,(IMGMODE)
	OR	A
	JR	NZ,PTL400
	LD	HL,(LINTOP)
	PUSH	HL
PTL200:	LD	A,(HL+)
	CP	13
	JR	Z,PTL300
	SRV	PUTCHR
	JR	PTL200
;
PTL300:	POP	HL
;        LD      (HL),A          ;WHEN (A=13) NOT NEED BY HIDE FLAG
PTL400:	SRV	PUTCRLF
	RET
;
;"EQU no value or PAGE lines
PTL500:	LD	(HL),"["
	INC	HL
	LD	DE,(NUM1)
	CALL	SET4H
	LD	(HL),"]"
	JR	PTL150
;"IF no cond
PTL600:	LD	A,(CONDIT)
	LD	DE,TRUMES
	OR	A
	JR	NZ,PTL610
	LD	DE,FALMES
PTL610:	LD	B,9
PTL620:	LD	A,(DE)
	INC	DE
	LD	(HL),A
	INC	HL
	DJNZ	PTL620
	JR	PTL150
PTL700:	LD	DE,IGNMES
	JR	PTL610
PTLERR:	LD	(HL+),"*"
	IF	FILEVER
	LD	DE,(ERRMES)
	EX	DE,HL
	LD	A,"@"
	LD	BC,0
	CPIR
	EX	DE,HL
PTLE10:	LD	A,(DE+)
	CP	32
	JR	Z,PTLE20
	OR	A
	JR	Z,PTLE20
	LD	(HL+),A
	JR	PTLE10
PTLE20:	LD	(HL+),"?"
	LD	(HL),"*"
	ENDIF
	JR	PTL150
;
TRUMES:	DEFB	"< TRUE > "
FALMES:	DEFB	"< FALSE >"
IGNMES:	DEFB	"(IGNORED)"
;
PUTDECSUB:	
	KEEP	AF,BC,DE,HL
	EX	DE,HL
	LD	HL,9999
	OR	A
	SBC	HL,DE
	JR	C,PDS40
	SRV	PUTSPC
PDS10:	LD	HL,999
	OR	A
	SBC	HL,DE
	JR	C,PDS40
	SRV	PUTSPC
PDS20:	LD	HL,99
	OR	A
	SBC	HL,DE
	JR	C,PDS40
	SRV	PUTSPC
PDS30:	LD	HL,9
	OR	A
	SBC	HL,DE
	JR	C,PDS40
	SRV	PUTSPC
PDS40:	EX	DE,HL
	SRV	PUTDEC
	LD	A,9
	SRV	PUTCHR
	KEEPOUT
	RET
;
;
SET4H:	LD	A,D
	CALL	SET2H
	LD	A,E
	JP	SET2H
;
SET2H:	PUSH	AF
	RRCA
	RRCA
	RRCA
	RRCA
	AND	$0F
	CALL	CNVHEX
	LD	(HL),A
	INC	HL
	POP	AF
	AND	$F
	CALL	CNVHEX
	LD	(HL),A
	INC	HL
	RET
;
CNVHEX:	CP	10
	JR	NC,CHX100
	ADD	$30
	RET
CHX100:	ADD	$41-10
	RET
;
FRMOBJ:	LD	C,0
	LD	A,(EXIMNM)
	OR	A
	JR	Z,FOBEEE
	LD	HL,OBJLINBUF
FOB100:	LD	A,(IDXBYT)
	OR	A
	JR	NZ,FOBOLD
	LD	A,(PREBYT)
        OR      A
	JR	Z,FOB210
	LD	(HL+),A
	INC	C
FOB210:	LD	A,(GIJFLG)
	OR	A
	JR	NZ,FOBEEE
        LD      A,(OPEBYT)
	LD	(HL+),A
	INC	C
FOB220:	LD	A,(DATWID)
	OR	A
	JR	Z,FOBEEE
	EX	AF,AF'
	LD	A,(DATBYT)
	LD	(HL+),A
	INC	C
	EX	AF,AF'
	CP	2
	LD	A,(DATBYT+1)
	JR	NZ,FOBEEE
	LD	(HL+),A
	INC	C
FOBEEE:	LD	A,C
	LD	(OBJCNT),A
	XOR	A
	LD	(FRMFLG),A
	RET
;
;
FOBOLD:	LD	HL,OBJLINBUF
	LD	C,0
	LD	A,(EXIMNM)
	OR	A
	JR	Z,FOBOEEE
FOBO100:	
	LD	A,(IDXBYT)
	LD	B,A
	OR	A
	CALL	NZ,FOBOSET
	LD	A,(PREBYT)
	OR	A
	CALL	NZ,FOBOSET
	LD	A,(GIJFLG)
	OR	A
	LD	A,(OPEBYT)
	CALL	Z,FOBOSET
	LD	A,(DATWID)
	OR	A
	JR	Z,FOBOEEE
	CP	2
	PUSH	AF
	LD	A,(DATBYT)
	CALL	FOBOSET
	POP	AF
	LD	A,(DATBYT+1)
	CALL	Z,FOBOSET
FOBOEEE:	
	LD	A,C
	LD	(OBJCNT),A
	XOR	A
	LD	(FRMFLG),A
	RET
;
FOBOSET:	
	LD	(HL),A
	INC	HL
	INC	C
	LD	A,B
	OR	A
	RET	Z
	LD	A,C
	CP	2
	LD	A,(OFSBYT)
	CALL	Z,FOBOSET
	RET
;
SETOPR:
	PUSH	AF
	LD	A,(OPEBYT)
	OR	B
	LD	(OPEBYT),A
	POP	AF
	LD	BC,SPR900
	PUSH	BC
	CP	6
	JR	Z,SPR100
	CP	7
	JR	Z,SPR200
	CP	8
	JR	Z,SPR500
	CP	12
	JR	Z,SPR300
	CP	13
	JR	Z,SPR600
	CP	14
	JR	Z,SPR200
	CP	16
	JR	Z,SPR400
	CP	32
	JR	Z,SPR100
	RET
SPR100:
	LD	A,1
SPR110:
	LD	(DATWID),A
	LD	(DATBYT),HL
	RET
SPR200:
	LD	A,2
	JR	SPR110
SPR300:
	LD	A,(OPEBYT)
	SLA	L
	SLA	L
	SLA	L
	OR	L
	LD	(OPEBYT),A
	RET
SPR400:
	LD	A,H
	OR	A
	JP	NZ,ILLOPR
	LD	A,L
	OR	A
	JR	Z,SPR300
	LD	L,2
	DEC	A
	JR	Z,SPR300
	LD	L,3
	DEC	A
	JR	Z,SPR300
	JP	ILLOPR
;
SPR500:
	LD	DE,(REFER)
	INC	DE
	INC	DE
	OR	A
	SBC	HL,DE
	PUSH	HL
	LD	DE,128
	ADD	HL,DE
	LD	A,H
	POP	HL
	OR	A
	JR	Z,SPR100
	LD	A,(PASS)
	DEC	A
	JR	Z,SPR100
	JP	OUTREL
;
SPR600:
	LD	A,H
	OR	A
	JP	NZ,ILLADR
	LD	A,L
	AND	$38
	CP	L
	JP	NZ,ILLADR
	LD	A,(OPEBYT)
	OR	L
	LD	(OPEBYT),A
	RET
;
SPR900:
	RET
;
;
;"if not collon after label,EQU only
EQUONL:	LD	A,(CRFFLG)
	OR	A
	CALL	NZ,PUTDNAM
	LD	A,(PASS)
	CP	2
	JR	Z,EON100
	CALL	FREEZE
	LD	HL,(IDPOS)
EON100:	KEEP	HL
	CALL	SSKIP_WSR
	LD	A,B
	OR	A
	JP	Z,ILLFRM
	LD	HL,72+$100
	OR	A
	SBC	HL,BC
	JP	NZ,ILLMNM
;
	LD	A,1
	LD	(EQUFLG),A
	CALL	EXPRES
	XOR	A
	LD	(EQUFLG),A
;
	LD	(NUM1),HL
	LD	C,L
	LD	B,H
	KEEPOUT
	LD	A,(PASS)
	CP	2
	JR	Z,EQUCOM
	LD	(IDPOS),HL
	CALL	SETLBL
EQUCOM:	LD	A,4
	LD	(GIJFLG),A
	JP	ALNEEE
;
SSKIP05:	
	INC	IY
SSKIP:	LD	A,(IY+0)
	CP	32
	JR	Z,SSKIP05
	CP	9
	RET	NZ
	JR	SSKIP05
;
KAMMA:	LD	A,(IY+0)
	INC	IY
	CP	","
	RET	Z
	JP	ILLFRM
;
	IF	HIGHSPEED
;
;  THIS IS THE BODY OF HIGH SPEED INSTRUCTION ANALIZER
.XLIST
	INCLUDE	zilog.aza
.LIST
;
TSTEOLX:	
;        IF      0
;        KEEP    AF
;        SRV     CPRNXT
;        DEFB    "EOF:",0
;        SRV     CPRDIR
;        SRV     CPRSPC
;        SRV     CPR2HEX
;        SRV     CPRCRLF
;        KEEPOUT
;        ENDIF
;
	CALL	SKIPSPC2
	LD	A,(HL+)
	JP	TSTEOL
;
SKIPSPC:	
	LD	A,(HL+)
SKIPSPC2:	
	CP	9
	JR	Z,SKPSPC1
	CP	" "
	JR	Z,SKPSPC1
	RET
SKPSPC1:
        LD      A,(HL+)
	CP	" "
	JR	Z,SKPSPC1
	CP	9
	JR	Z,SKPSPC1
	DEC	HL
	XOR	A
	RET
;
N_EXP2:	DEC	HL
N_EXP:	CALL	THEXPRES
	LD	(DATBYT),DE
	LD	A,1
	LD	(DATWID),A
	RET
;
NN_EXP2:	DEC	HL
NN_EXP:	CALL	THEXPRES
	LD	(DATBYT),DE
	LD	A,2
	LD	(DATWID),A
	RET
;
E_EXP2:	DEC	HL
E_EXP:	CALL	THEXPRES
	PUSH	HL
	EX	DE,HL
	LD	DE,(REFER)
	INC	DE
	INC	DE
	OR	A
	SBC	HL,DE
	PUSH	HL
	LD	DE,128
	ADD	HL,DE
	LD	A,H
	POP	HL
	OR	A
	JR	Z,E_EXP10
	LD	A,(PASS)
	DEC	A
	JP	NZ,OUTREL
E_EXP10:	
	LD	(DATBYT),HL
	LD	A,1
	LD	(DATWID),A
	POP	HL
	RET
;
D_EXP2:	DEC	HL
D_EXP:	JP	INSERR
	IF	0
	CALL	THEXPRES
	EX	DE,HL
	PUSH	HL
	LD	BC,128
	ADD	HL,BC
	LD	A,H
	POP	HL
	OR	A
	JP	NZ,OUTOFS
	LD	A,L
	LD	(OFSBYT),A
	EX	DE,HL
	ENDIF
;
THEXPRES:	
	KEEP	IY
;!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
	IF	0
	KEEP	AF
	SRV	CPRNXT
	DEFB	"ENTEXP:",0
	LD	A,(HL)
	SRV	CPRDIR
	SRV	CPRSPC
	SRV	CPR2HEX
        SRV     CPRCRLF
	KEEPOUT
	ENDIF
;
	PUSH	HL
	POP	IY
	CALL	EXPRES
	PUSH	IY
        EX      DE,HL
	POP	HL
	KEEPOUT
	RET
;
;
;
;UNTIL HERE (EXPANDED FOR VER 3.0)
	ENDIF
;
;"Errors
ILLMNM:	LD	HL,IMNMES
	JR	ERRORS
ILLFRM:	LD	HL,IFMMES
	JR	ERRORS
ILLNAM:	LD	HL,INAMES
	JR	ERRORS
ILLOPR:	LD	HL,IOPMES
	JR	ERRORS
ILLNUM:	LD	HL,INUMES
	JR	ERRORS
ILLADR:	LD	HL,IADMES
	JR	ERRORS
ILLKAK:	LD	HL,IKKMES
	JR	ERRORS
TRMMIS:	LD	HL,TMSMES
	JR	ERRORS
UNDLBL:	LD	HL,ULBMES
	JR	ERRORS
UDFLBL:	LD	HL,UDLMES
	JR	ERRORS
DBLNAM:	LD	HL,DNAMES
	JR	ERRORS
AFTWRT:	LD	HL,AFTMES
	JR	ERRORS
OUTOFS:	LD	HL,OOFMES
	JR	ERRORS
OUTREL:	LD	HL,ORLMES
	JR	ERRORS
STKMIS:	LD	HL,STMMES
	JR	ERRORS
NOEND:	LD	HL,NOEMES
	JP	ERRORF
OUTIDT:	LD	HL,OTBMES
	JP	ERRORF
STKOVR:	LD	HL,STOMES
	JP	ERRORF
INCERR:	LD	HL,INCMES
	JR	ERRORS
PPERR:	LD	HL,PPMES
	JR	ERRORS
INTERR:	LD	HL,IERMES
	JP	ERRORF
TMLERR:	LD	HL,TMLERM
	JP	ERRORF
STMISX:	LD	HL,SMXERM
	JP	ERRORF
ILLCND:	LD	HL,ILCERM
;        JR      ERRORS
ERRORS: IF      HIGHSPEED
	LD	A,(HMODE)
	OR	A
	JP	NZ,INSERR
	ENDIF
	SRV	CPRCRLF
	PUSH	HL
	LD	HL,(LINTOP)
ERR100:	LD	A,(HL)
	INC	HL
	SRV	CPRCHR
	CP	13
	JR	NZ,ERR100
	SRV	CPRCRLF
	POP	HL
	PUSH	HL
ERR200:	LD	A,(HL+)
	CP	"@"
	JR	Z,ERR200
	OR	A
	JR	Z,ERR300
	SRV	CPRCHR
	JR	ERR200
ERR300:	POP	HL
	LD	A,7
	SRV	CPRCHR
	SRV	CPRCRLF
;
	IF	FILEVER
	LD	(ERRMES),HL
	LD	HL,3
	LD	(OBJSKP),HL
	XOR	A
	LD	(OBJCNT),A
	LD	(EXIMNM),A
	DEC	A
	LD	(ERRORED),A
        CALL    PUTFUN
	XOR	A
	LD	(ERRORED),A
        LD      HL,(ERRCNT)
	INC	HL
	LD	(ERRCNT),HL
	LD	HL,(FILEP)
	LD	(ERRMARK),HL
ERR400:	LD	A,(STOPFLG)
	OR	A
	JP	Z,NEXTLINE
;
	ELSE
	LD	IX,SRCFCB
	LD	L,(IX+18)
	LD	H,(IX+19)
	LD	(POINTR),HL
	ENDIF
;
	SRV	RESET
	SRV	WARM
;
ERRORF:	SRV	CPRCRLF
	SRV	CPRSTR
	LD	A,7
	SRV	CPRCHR
	SRV	CPRCRLF
	SRV	RESET
	SRV	WARM
;
IMNMES:	DEFB	"Illegal @mnemonic name",0
IFMMES:	DEFB	"Illegal Source written @Format",0
INAMES:	DEFB	"Illegal @Name",0
IOPMES:	DEFB	"Illegal @Operand",0
INUMES:	DEFB	"Illegal @Number",0
IADMES:	DEFB	"Illegal @Address",0
IKKMES:	DEFB	"@KAKKO not right close",0
TMSMES: DEFB    "@Terminater Not Match",0
ULBMES:	DEFB	"Label Undefined @before",0
UDLMES:	DEFB	"@Undefined Label",0
DNAMES:	DEFB	"Name @Double Defined",0
AFTMES:	DEFB	"@WARNING! Something was written after instruction",0
OOFMES:	DEFB	"Out of Index adressing @ofset",0
ORLMES:	DEFB	"Out of relative addressing @ofset",0
STMMES:	DEFB	"@Nesting Missed",0
INCMES:	DEFB	"INCLUDE in @Included file",0
PPMES:	DEFB	"@PRE/POST-INC/DEC Error",0
ILCERM:	DEFB	"Uncalculatable @Condition",0
;
IERMES:	DEFB	"Internal Error!!  Need to Debug",0
STOMES:	DEFB	"Nesting Stack Over flow",0
OTBMES:	DEFB	"Out of I.D. table area",0
NOEMES:	DEFB	"No END befor End of File",0
TMLERM:	DEFB	"Too Many Labels",0
SMXERM:	DEFB	"Nesting not closed!",0
;
;"if Z then EOL
TSTEOL:
	CP	13
	RET	Z
	CP	";"
	RET
;
	IF	0
TSTEOLX:
	LD	A,B
	OR	A
	RET	NZ
	LD	A,C
	JR	TSTEOL
	ENDIF
;
UPPER:
	CP	"a"
	RET	C
	CP	"z"+1
	RET	NC
	SUB	32
	RET
;
;
;"if C then error
TESTAZ:
	CP	"@"
	RET	C
	CP	"Z"+1
	CCF
	RET
;
TEST09:
	CP	"0"
	RET	C
	CP	"9"+1
	CCF
	RET
;
TESTAN:
	CALL	TESTAZ
	RET	NC
	CP	"."
	RET	Z
	CP	"_"
	RET	Z
	CP	"$"
	RET	Z
	CP	"?"
	RET	Z
	CALL	TEST09
	RET
;
PRFN:	PUSH	IX
	POP	HL
	INC	HL
	LOOP	B,8
	LD	A,(HL+)
	SRV	CPRCHR
	LEND
	LD	A,"."
	SRV	CPRCHR
	LOOP	B,3
	LD	A,(HL+)
	SRV	CPRCHR
	LEND
	RET
;
;"Stack operations
STKINI:	LD	HL,STKEND
	LD	(SPS),HL
	XOR	A
	LD	(STKUSE),A
	RET
;
STKCLOS:
	LD	A,(STKUSE)
	IF	0
	KEEP	AF
	SRV	CPR2HEX
	KEEPOUT
	ENDIF

	OR	A
	RET	Z
	JP	STMISX
;
STKLOOK:
	PUSH	AF
	PUSH	IY
	LD	A,(STKUSE)
	OR	A
	JP	Z,STKMIS
	LD	IY,(SPS)
	LD	H,(IY+1)
	LD	L,(IY+0)
	POP	IY
	POP	AF
	RET
;
STKPOP:	
	IF	0
	SRV	CPRNXT
	DEFB	"POP!",0
	ENDIF
;
	PUSH	IY
	LD	IY,(SPS)
	CALL	STKLOOK
	INC	IY
	INC	IY
	LD	(SPS),IY
;       PUSH    HL
;        PUSH    DE
;        LD      HL,(SPS)
;        LD      DE,STKEND
;        OR      A
;        SBC     HL,DE
;        JR      NZ,STKPO1
	PUSH	AF
	LD	A,(STKUSE)
	DEC	A
	LD	(STKUSE),A
	POP	AF
;STKPO1: POP     DE
;        POP     HL
	POP	IY
	RET
;
STKPUSH:	
	IF	0
	SRV	CPRNXT
	DEFB	"PUSH!",0
	ENDIF
;
	PUSH	AF
	PUSH	IY
	PUSH	DE
	PUSH	HL
	LD	HL,(SPS)
	LD	DE,STKSTR
	OR	A
	SBC	HL,DE
	JP	Z,STKOVR
	LD	IY,(SPS)
	POP	HL
	LD	(IY-1),H
	LD	(IY-2),L
	DEC	IY
	DEC	IY
	LD	(SPS),IY
	LD	A,(STKUSE)
	INC	A
	LD	(STKUSE),A
	POP	DE
	POP	IY
	POP	AF
	RET
;
SETLBL:	LD	HL,(IDPOS)
	LD	(HL),$F1
	INC	HL
	LD	(HL),C
	INC	HL
	LD	(HL),B
	RET
;
SETAZA:	LD	A,(IX+9)
	CP	32
	RET	NZ
	LD	(IX+9),"a"
	LD	(IX+10),"z"
	LD	(IX+11),"a"
	RET
;
;
SETIMG:	LD	A,(IX+9)
	CP	32
	RET	NZ
	LD	(IX+9),"i"
	LD	(IX+10),"m"
	LD	(IX+11),"g"
	RET
;
WORKS:
;
LINBUF:	DEFS	81
LSTBUF:	DEFS	80
OBJLINBUF:	DEFS	64
;
OBJFLG:	DEFS	1
LSTFLG:	DEFS	1
XLSTFLG:	DEFS	1
XCRFFLG:	DEFS	1
ATTFLG:	DEFS	1
NIFFLG:	DEFS	1
LBLFLG:	DEFS	1
	IF	FILEVER
SYMFLG:	DEFS	1
DEBFLG:	DEFS	1
RUNFLG:	DEFS	1
STOPFLG:	DEFS	1
MOBJFLG:	DEFS	1
	ENDIF
;
PASS:	DEFS	1
;POINT:  DEFS    2
REFER:	DEFS	2
OBJREF:	DEFS	2
BUFREF:	DEFS	2
OBJOFF:	DEFS	2
STARTADR:	DEFS	2	;FPUT START
IMGMODE:	DEFS	1
TWNFLG:	DEFS	1
;
WORKS_S:	
IDXBYT:	DEFS	1
PREBYT:	DEFS	1
OPEBYT:	DEFS	1
OFSBYT:	DEFS	1
DATBYT:	DEFS	2
DATWID:	DEFS	1
;
PRECODE:
PREINC:	DEFS	1
	DEFS	2
PREDEC:	DEFS	1
	DEFS	2
	DEFS	3*2
POSTCODE:
POSTINC:	DEFS	1
	DEFS	2
POSTDEC:	DEFS	1
	DEFS	2
	DEFS	3*2
;
HIDELST:	DEFS	1
PP_EXIST:	DEFS	1
WORKS_I:
;
EQUFLG:	DEFS	1
GIJFLG:	DEFS	1
COLLES:	DEFS	1
EXIMNM:	DEFS	1
PPFLG:	DEFS	1
FRMFLG:	DEFS	1
ONCEXLST:	DEFS	1
HMODE:	DEFS	1
OBJSKP:	DEFS	2
WORKS_E:	
;
KEPNUM:	DEFS	2
NXTWK:	DEFS	2
NUM1:	DEFS	2
NUM2:	DEFS	2
OP1:	DEFS	1
OP2:	DEFS	1
ORP1:	DEFS	1
ORP2:	DEFS	1
TBL1:	DEFS	2
TBL2:	DEFS	2
ORGFLG:	DEFS	1
OBJCNT:	DEFS	1
LINTOP:	DEFS	2
FILEP:	DEFS	2
EOFFLG:	DEFS	1
NOWIDX:	DEFS	1
WKEEP:	DEFS	2
TKEEP:	DEFS	1
LSTPOS:	DEFS	2
CALMODE:	DEFS	1
INCFLG:	DEFS	1
CONDIT:	DEFS	2
IFNEST:	DEFS	1
FCBA:	DEFS	2
BUFA:	DEFS	2
MLEWK:	DEFS	1
SPKEEP:	DEFS	2
PTRKEEP:	DEFS	2
NOBUF:	DEFS	1
;
SRCFCB:	DEFS	36+7
INCFCB:	DEFS	36+7
;
IMGFCB:	DEFS	36
;
LSTFCB:	DEFS	36
CRFFCB:	DEFS	36
SYMFCB:	DEFS	36
;
SPS:	DEFS	2
STKUSE:	DEFS	1
	DEFS	2		;FOR DUMMY
;
	IF	FILEVER
STKSIZ	EQU	120
STKSTR:	DEFS	STKSIZ
STKEND:
	ELSE
STKSIZ	EQU	60
STKSTR	EQU	$E980
STKEND	EQU	STKSTR+STKSIZ
	ENDIF
;
;
	IF	FILEVER
HSPKEEP:	DEFS	2
HPTRKEEP:	DEFS	2
ERRCNT:	DEFS	2
ERRMES:	DEFS	2
ERRORED:	DEFS	1
DMYFCB:	DEFS	36		;THIS IS DUMMY.
SUBTTLBUF:	DEFS	61
PAGES:	DEFS	1
PGCOUNT:	DEFS	1
PGNUM:	DEFS	2
DATEBUF:	DEFS	6
OBJFCB:	DEFS	36
FLSTBUF:	DEFS	256
FCRFBUF:	DEFS	256
FSYMBUF:	DEFS	256
CRFFLG: DEFB    0
FIRSTFF:	DEFS	1
LINENUM:	DEFS	2
LNFLG:	DEFS	1
;
SRCBUFBYTES	EQU	BUFSIZE*256*8
BUF     EQU     $
BUF2	EQU	$+SRCBUFBYTES
FOBJBUF	EQU	BUF2+SRCBUFBYTES
LABELSTART	EQU	FOBJBUF+2048
LABELEND	EQU	LABELSTART+4096
SORTBUF	EQU	BUF		;(AS 2048*2 BYTES)
;BUF:DEFS 2048
;BUF2:DEFS 2048
;LABELSIZE       EQU     $4000
;LABELSTART:DEFS LABELSIZE
;LABELEND:
	ELSE
FLSTBUF	EQU	$F300
BUF	EQU	$F300
	IF	VER8
BUF2	EQU	$C800
LABELEND	EQU	$C7FF
	ELSE
BUF2	EQU	$E000
LABELEND	EQU	$C1FF
	ENDIF
LABELSTART	EQU	$B000
	ENDIF
;
ZZZZZZ:
.DEPHASE
;
;        ORG     WORKS
	END
