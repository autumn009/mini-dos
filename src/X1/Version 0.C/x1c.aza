;
; X1 CONSOLE OUTPUT MODULE
;
;
TVRAM	EQU	$3000
AVRAM	EQU	$2000
;
; CONOUT INPUT A: chr
;        DESTROY NONE
CONOUT0:	
	KEEP	AF,BC,DE,HL
	CP	7
	JP	Z,COBELL
	CP	9
	JP	Z,COTAB
	CP	10
	JP	Z,COLF
	CP	11
	JP	Z,COHOME
	CP	12
	JP	Z,COCLR
	CP	13
	JP	Z,COCR
	CP	28
	JP	Z,CORIGHT
	CP	29
	JP	Z,COLEFT
	CP	30
	JP	Z,COUP
	CP	31
	JP	Z,CODOWN
;
; NORMAL CHR
	KEEP	AF
;
	LD	BC,(CURPAIR)
	CALL	CALOFF
;
	LD	DE,TVRAM
	ADD	HL,DE
;
	KEEPOUT
	LD	C,L
	LD	B,H
	OUT	(C),A
;
CORIGHT:	
	LD	A,(CURX)
	INC	A
	CP	80
	LD	(CURX),A
	JP	NZ,COEEE
	XOR	A
	LD	(CURX),A
CODOWN:	
COLF:	
	LD	A,(CURY)
	INC	A
	CP	25
	JR	Z,SCROLL
	LD	(CURY),A
	JP	COEEE
;
SCROLL:	
	LD	BC,TVRAM
	EXX
	LD	BC,TVRAM+80
	EXX
	LOOP	DE,$800-80
	EXX
	IN	A,(C)
	INC	BC
	EXX
	OUT	(C),A
	INC	BC
	LEND	A
	LD	BC,TVRAM+1920
	LD	D," "
	LOOP	E,80
	OUT	(C),D
	INC	BC
	LEND
	JP	COEEE
;
COCR:	XOR	A
	LD	(CURX),A
	JP	COEEE
;
COBELL:	LD	A,$60
	CALL	SBELL		;SRV 58 ;BELL
	JP	COEEE
;
COTAB:	LD	A," "
	CALL	CONOUT
	LD	A,(CURX)
	AND	7
	JR	NZ,COTAB
	JP	COEEE
;
COLEFT:	
	LD	A,(CURX)
	OR	A
	JR	Z,COLEFT10
	DEC	A
	LD	(CURX),A
	JP	COEEE
COLEFT10:	
	LD	A,79
	LD	(CURX),A
COUP:	LD	A,(CURY)
	OR	A
	JP	Z,COEEE
	DEC	A
	LD	(CURY),A
	JP	COEEE
;
;
COCLR:	
	LD	BC,AVRAM
	LD	L,7
	LOOP	DE,$800
	OUT	(C),L
	INC	BC
	LEND	A
	LD	BC,TVRAM
	LD	L," "
	LOOP	DE,$800
	OUT	(C),L
	INC	BC
	LEND	A
;
COHOME:	XOR	A
	LD	(CURX),A
	LD	(CURY),A
	JP	COEEE
;
;
;
;
COEEE:	
	KEEPOUT
	RET
;
;
CURON:	KEEP	AF,BC,DE,HL
	LD	BC,(CURPAIR)
	CALL	AADR
	IN	A,(C)
	LD	(CURATR),A
	OR	$18
	OUT	(C),A
	KEEPOUT
	RET
;
CUROFF:	KEEP	AF,BC,DE,HL
	LD	BC,(CURPAIR)
	CALL	AADR
	LD	A,(CURATR)
	OUT	(C),A
	KEEPOUT
	RET
;
CURATR:	DB	0
;
;
AADR:	CALL	CALOFF
	LD	DE,AVRAM
	ADD	HL,DE
	LD	C,L
	LD	B,H
	RET
;
CALOFF:	
	LD	L,C
	LD	H,0
	LD	E,L
	LD	D,H
;
	ADD	HL,HL
	ADD	HL,HL
	ADD	HL,DE
	ADD	HL,HL
;
	ADD	HL,HL
	ADD	HL,HL
	ADD	HL,HL
;
	LD	E,B
	LD	D,0
	ADD	HL,DE
;
	RET
;
;
;
CURPAIR:	
CURY:	DB	0
CURX:	DB	0
;
; END OF INCLUDED MODULE 'x1c.aza'
